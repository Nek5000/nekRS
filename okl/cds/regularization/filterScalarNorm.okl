/*

   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */

@kernel void filterScalarNorm(const dlong Nelements,
                              const dlong is,
                              const dlong offset,
                              @restrict const dfloat* fMT,
                              const dfloat sensorSensitivity,
                              const dfloat sensorOrder,
                              const dfloat p,
                              @restrict const dfloat* U,
                              @restrict dfloat* logShockSensor,
                              @restrict dfloat* filteredResults)
{
  for(dlong e = 0; e < Nelements; ++e; @outer(0)) { // for all elements
    @shared dfloat s_U[p_Nq][p_Nq];
    @shared dfloat s_FT[p_Nq][p_Nq];
    @shared dfloat s_filtered[p_blockSize];
    @shared dfloat s_unfiltered[p_blockSize];

    @exclusive dfloat r_Un[p_Nq];
    @exclusive dfloat r_U[p_Nq];

    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int id = i + j * p_Nq;
        s_FT[0][id] = fMT[id + is * p_Nq * p_Nq];

#pragma unroll p_Nq
        for(int k = 0; k < p_Nq; ++k)
          r_Un[k] = 0.f;

        for (int k = j*p_Nq + i; k < p_blockSize; k += p_Nq*p_Nq){
          s_filtered[k] = 0.0;
          s_unfiltered[k] = 0.0;
        }
      }
    }

    @barrier("local");

    //read and multiply in k
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
#pragma unroll p_Nq
        for(int k = 0; k < p_Nq; ++k) {
          const dlong id = e * p_Np + k * p_Nq * p_Nq + j * p_Nq + i;
          const dfloat uc = U[id + offset];

          // Could be hold for less global reads
          r_U[k] = uc;

#pragma unroll p_Nq
          for(int n = 0; n < p_Nq; ++n) {
            const dfloat Ik = s_FT[k][n];
            r_Un[n] += Ik * uc;
          }
        }
      }
    }

    @barrier("local");

// loop around slices
#pragma unroll p_Nq
    for(int k = 0; k < p_Nq; ++k) {
      //load slice to @shared
      for(int j = 0; j < p_Nq; ++j; @inner(1))
        for(int i = 0; i < p_Nq; ++i; @inner(0))
          s_U[j][i] = r_Un[k];

      @barrier("local");

      for(int j = 0; j < p_Nq; ++j; @inner(1)) {
        for(int i = 0; i < p_Nq; ++i; @inner(0)) {
          r_Un[k] = 0.f;
#pragma unroll p_Nq
          for (int n = 0; n < p_Nq; n++) {
            const dfloat Ii = s_FT[n][i];
            r_Un[k] += Ii * s_U[j][n];
          }
        }
      }

      @barrier("local");
      //load multiplied data to shared
      for(int j = 0; j < p_Nq; ++j; @inner(1))
        for(int i = 0; i < p_Nq; ++i; @inner(0))
          s_U[j][i] = r_Un[k];


      @barrier("local");

      for(int j = 0; j < p_Nq; ++j; @inner(1)) {
        for(int i = 0; i < p_Nq; ++i; @inner(0)) {
          r_Un[k] = 0.f;

#pragma unroll p_Nq
          for (int n = 0; n < p_Nq; n++) {
            const dfloat Ij = s_FT[n][j];
            r_Un[k] += Ij * s_U[n][i];
          }
        }
      }

      @barrier("local");
    }

    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
#pragma unroll p_Nq
        for(int k = 0; k < p_Nq; ++k) {
          const int tid = j * p_Nq + i;
          const dlong id = e * p_Np + k * p_Nq * p_Nq + j * p_Nq + i;
          const dfloat unfilteredVal = r_U[k];
          const dfloat filteredDiff = unfilteredVal - r_Un[k];

          filteredResults[id] = filteredDiff;

          s_filtered[tid] += filteredDiff * filteredDiff;
          s_unfiltered[tid] += unfilteredVal*unfilteredVal;
        }
      }
    }
    @barrier("local");
    // reduce p_Nq * p_Nq to single value
    #if p_blockSize > 512
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 512){
          s_filtered[t] += s_filtered[t+512];
          s_unfiltered[t] += s_unfiltered[t+512];
        }
      }
    }
    @barrier("local");
    #endif
    #if p_blockSize > 256
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 256){
          s_filtered[t] += s_filtered[t+256];
          s_unfiltered[t] += s_unfiltered[t+256];
        }
      }
    }
    @barrier("local");
    #endif
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 128){
          s_filtered[t] += s_filtered[t+128];
          s_unfiltered[t] += s_unfiltered[t+128];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 64){
          s_filtered[t] += s_filtered[t+64];
          s_unfiltered[t] += s_unfiltered[t+64];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 32){
          s_filtered[t] += s_filtered[t+32];
          s_unfiltered[t] += s_unfiltered[t+32];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 16){
          s_filtered[t] += s_filtered[t+16];
          s_unfiltered[t] += s_unfiltered[t+16];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 8){
          s_filtered[t] += s_filtered[t+8];
          s_unfiltered[t] += s_unfiltered[t+8];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 4){
          s_filtered[t] += s_filtered[t+4];
          s_unfiltered[t] += s_unfiltered[t+4];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 2){
          s_filtered[t] += s_filtered[t+2];
          s_unfiltered[t] += s_unfiltered[t+2];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 1){
          const dfloat filteredNorm = s_filtered[0] + s_filtered[1];
          const dfloat unfilteredNorm = s_unfiltered[0] + s_unfiltered[1];
          const dfloat S = filteredNorm / unfilteredNorm;
          const dfloat Ftmp = pow(p, sensorOrder) * sensorSensitivity * S;
          const dfloat F = (1 < Ftmp) ? 1 : Ftmp;
          logShockSensor[e] = log10(F);

        }
      }
    }
  }
}
