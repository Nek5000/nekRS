/*

   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */

@kernel void sumMakef(const dlong N,
                      @ restrict const dfloat *massMatrix,
                      const dfloat idt,
                      @ restrict const dfloat *coeffEXT,
                      @ restrict const dfloat *coeffBDF,
                      const dlong fieldOffset,
                      @ restrict const dfloat *U,
                      @ restrict const dfloat *NU,
                      @ restrict const dfloat *FU,
                      @ restrict dfloat *BF)
{
  for (dlong id = 0; id < N; ++id; @tile(p_blockSize, @outer, @inner)) {
    dfloat JW = massMatrix[id];

    dfloat bfx = 0;
    dfloat bfy = 0;
    dfloat bfz = 0;

#if p_SUBCYCLING
    const dfloat NUx = NU[id + 0 * fieldOffset];
    const dfloat NUy = NU[id + 1 * fieldOffset];
    const dfloat NUz = NU[id + 2 * fieldOffset];
    bfx += idt * NUx;
    bfy += idt * NUy;
    bfz += idt * NUz;

    for (int s = 0; s < p_nEXT; s++) {
      const dfloat FUx = FU[id + 0 * fieldOffset + s * p_NVfields * fieldOffset];
      const dfloat FUy = FU[id + 1 * fieldOffset + s * p_NVfields * fieldOffset];
      const dfloat FUz = FU[id + 2 * fieldOffset + s * p_NVfields * fieldOffset];
#if p_MovingMesh
      JW = massMatrix[id + s * fieldOffset];
#endif
      bfx += JW * coeffEXT[s] * FUx;
      bfy += JW * coeffEXT[s] * FUy;
      bfz += JW * coeffEXT[s] * FUz;
    }
#else
    for (int s = 0; s < p_nEXT; s++) {
      const dfloat FUx = FU[id + 0 * fieldOffset + s * p_NVfields * fieldOffset];
      const dfloat FUy = FU[id + 1 * fieldOffset + s * p_NVfields * fieldOffset];
      const dfloat FUz = FU[id + 2 * fieldOffset + s * p_NVfields * fieldOffset];
#if p_MovingMesh
      JW = massMatrix[id + s * fieldOffset];
#endif
      bfx += JW * coeffEXT[s] * FUx;
      bfy += JW * coeffEXT[s] * FUy;
      bfz += JW * coeffEXT[s] * FUz;
    }
    for (int s = 0; s < p_nBDF; s++) {
      const dfloat Um = U[id + 0 * fieldOffset + s * p_NVfields * fieldOffset];
      const dfloat Vm = U[id + 1 * fieldOffset + s * p_NVfields * fieldOffset];
      const dfloat Wm = U[id + 2 * fieldOffset + s * p_NVfields * fieldOffset];
#if p_MovingMesh
      JW = massMatrix[id + s * fieldOffset];
#endif
      bfx += JW * (idt * coeffBDF[s] * Um);
      bfy += JW * (idt * coeffBDF[s] * Vm);
      bfz += JW * (idt * coeffBDF[s] * Wm);
    }
#endif

    BF[id + 0 * fieldOffset] = bfx;
    BF[id + 1 * fieldOffset] = bfy;
    BF[id + 2 * fieldOffset] = bfz;
  }
}
