/*

   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */

@kernel void ellipticPreconProlongateQuad2D(const dlong Nelements,
                                            @restrict const dfloat*  R,
                                            @restrict const dfloat*  qc,
                                            @restrict dfloat*  qN)
{
  for(dlong e = 0; e < Nelements; ++e; @outer(0)) {
    @shared dfloat s_qcf[p_NqCoarse][p_NqFine];
    @shared dfloat s_qcc[p_NqCoarse][p_NqCoarse];
    @shared dfloat s_R[p_NqCoarse][p_NqFine];

    // prefetch to @shared
    for(int j = 0; j < p_NqFine; ++j; @inner(1))
      for(int i = 0; i < p_NqFine; ++i; @inner(0)) {
        const int id = i + j * p_NqFine;
        if(id < p_NpCoarse)
          s_qcc[0][id] = qc[id + e * p_NpCoarse];
        if(id < p_NqCoarse * p_NqFine)
          s_R[0][id] = R[id];
      }

    @barrier("local");

    // prolongate in i index
    for(int j = 0; j < p_NqFine; ++j; @inner(1)) {
      for(int i = 0; i < p_NqFine; ++i; @inner(0)) {
        if(j < p_NqCoarse) {
          dfloat res = 0;
#pragma unroll p_NqCoarse
          for(int m = 0; m < p_NqCoarse; ++m)
            res += s_R[m][i] * s_qcc[j][m];
          s_qcf[j][i] = res;
        }
      }
    }

    @barrier("local");

    // coarsen in i index
    for(int j = 0; j < p_NqFine; ++j; @inner(1)) {
      for(int i = 0; i < p_NqFine; ++i; @inner(0)) {
        dfloat res = 0;
#pragma unroll p_NqCoarse
        for(int m = 0; m < p_NqCoarse; ++m)
          res += s_R[m][j] * s_qcf[m][i];

        const dlong id = i + j * p_NqFine + e * p_NpFine;
        qN[id] += res;
      }
    }
  }
}