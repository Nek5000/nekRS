/*

   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */
@kernel void strongAdvectionVolumeHex3D(const dlong Nelements,
                                           @restrict const dfloat*  D,
                                           const dlong offset,
                                           @restrict const dfloat*  U,
                                           @restrict const dfloat* Urst,
                                           @restrict dfloat*  NU)
{
  for(dlong e = 0; e < Nelements; ++e; @outer(0)) {
    @shared dfloat s_U[p_Nq][p_Nq];
    @shared dfloat s_V[p_Nq][p_Nq];
    @shared dfloat s_W[p_Nq][p_Nq];
    @exclusive dfloat s_Uloc[p_Nq];
    @exclusive dfloat s_Vloc[p_Nq];
    @exclusive dfloat s_Wloc[p_Nq];

    @shared dfloat s_D[p_Nq][p_Nq];

    #pragma unroll p_Nq
    for(int k = 0; k < p_Nq; ++k){
      for(int j = 0; j < p_Nq; ++j; @inner(1)){
        for(int i = 0; i < p_Nq; ++i; @inner(0)) {
          dlong id = i + j * p_Nq;
          if(k == 0)
            s_D[0][id] = D[id];

          id = e * p_Np + k * p_Nq * p_Nq + j * p_Nq + i;

          const dfloat Un = U[id + 0 * offset];
          const dfloat Vn = U[id + 1 * offset];
          const dfloat Wn = U[id + 2 * offset];

          s_U[j][i] = Un;
          s_V[j][i] = Vn;
          s_W[j][i] = Wn;
          if(k == 0){
            #pragma unroll p_Nq
            for(int l = 0 ; l < p_Nq; ++l){
              const dlong other_id = e * p_Np + l * p_Nq * p_Nq + j * p_Nq + i;
              s_Uloc[l] = U[other_id + 0 * offset];
              s_Vloc[l] = U[other_id + 1 * offset];
              s_Wloc[l] = U[other_id + 2 * offset];
            }
          }
        }
      }

    @barrier("local");

      for(int j = 0; j < p_Nq; ++j; @inner(1)) {
        for(int i = 0; i < p_Nq; ++i; @inner(0)) {

          dfloat dudr = 0.0;
          dfloat duds = 0.0;
          dfloat dudt = 0.0;

          dfloat dvdr = 0.0;
          dfloat dvds = 0.0;
          dfloat dvdt = 0.0;

          dfloat dwdr = 0.0;
          dfloat dwds = 0.0;
          dfloat dwdt = 0.0;
#pragma unroll p_Nq
          for (int n = 0; n < p_Nq; n++) {
            const dfloat Dr = s_D[i][n];
            const dfloat Ds = s_D[j][n];
            const dfloat Dt = s_D[k][n];
            dudr += Dr * s_U[j][n];
            duds += Ds * s_U[n][i];
            dudt += Dt * s_Uloc[n];

            dvdr += Dr * s_V[j][n];
            dvds += Ds * s_V[n][i];
            dvdt += Dt * s_Vloc[n];

            dwdr += Dr * s_W[j][n];
            dwds += Ds * s_W[n][i];
            dwdt += Dt * s_Wloc[n];
          }

          const dlong id = e * p_Np + k * p_Nq * p_Nq + j * p_Nq + i;
          const dfloat Uhat = Urst[id + 0 * offset];
          const dfloat Vhat = Urst[id + 1 * offset];
          const dfloat What = Urst[id + 2 * offset];

          NU[id + 0 * offset] =  (Uhat * dudr + Vhat * duds + What * dudt);
          NU[id + 1 * offset] =  (Uhat * dvdr + Vhat * dvds + What * dvdt);
          NU[id + 2 * offset] =  (Uhat * dwdr + Vhat * dwds + What * dwdt);
        }
      }
    }
  }
}

@kernel void strongAdvectionCubatureVolumeHex3D(const dlong Nelements,
                                                   @restrict const dfloat*  vgeo,
                                                   @restrict const dfloat*  cubD,
                                                   @restrict const dfloat*  cubInterpT,
                                                   @restrict const dfloat*  cubProjectT,
                                                   const dlong offset,
                                                   const dlong cubatureOffset,
                                                   @restrict const dfloat*  U,
                                                   @restrict const dfloat* Urst,
                                                   @restrict dfloat*  NU)
{
  // (phi, U.grad Ud)
  for(dlong e = 0; e < Nelements; ++e; @outer(0)) {
    @shared dfloat s_cubD[p_cubNq][p_cubNq];
    @shared dfloat s_cubInterpT[p_Nq][p_cubNq];
    @shared dfloat s_cubProjectT[p_cubNq][p_Nq];

    @shared dfloat s_U[p_cubNq][p_cubNq];
    @shared dfloat s_V[p_cubNq][p_cubNq];
    @shared dfloat s_W[p_cubNq][p_cubNq];

    @shared dfloat s_U1[p_cubNq][p_cubNq];
    @shared dfloat s_V1[p_cubNq][p_cubNq];
    @shared dfloat s_W1[p_cubNq][p_cubNq];

    @exclusive dfloat r_U[p_cubNq], r_V[p_cubNq], r_W[p_cubNq];
    @exclusive dfloat r_Ud[p_cubNq], r_Vd[p_cubNq], r_Wd[p_cubNq];

    for(int j = 0; j < p_cubNq; ++j; @inner(1))
      for(int i = 0; i < p_cubNq; ++i; @inner(0)) {
        const int id = i + j * p_cubNq;
        if (id < p_Nq * p_cubNq) {
          s_cubInterpT[0][id] = cubInterpT[id];
          s_cubProjectT[0][id] = cubProjectT[id];
        }

        s_cubD[0][id] = cubD[id];

        for(int k = 0; k < p_cubNq; ++k) {
          r_U[k]  = 0;
          r_V[k]  = 0;
          r_W[k]  = 0;
          r_Ud[k] = 0;
          r_Vd[k] = 0;
          r_Wd[k] = 0;
        }
      }

    for(int c = 0; c < p_Nq; ++c) {
      @barrier("local");

      for(int b = 0; b < p_cubNq; ++b; @inner(1))
        for(int a = 0; a < p_cubNq; ++a; @inner(0))
          if(a < p_Nq && b < p_Nq) {
            // this can be improved
            const dlong id = e * p_Np + c * p_Nq * p_Nq + b * p_Nq + a;
            // U.grad U
            s_U[b][a] = U[id + 0 * offset];
            s_V[b][a] = U[id + 1 * offset];
            s_W[b][a] = U[id + 2 * offset];
          }

      @barrier("local");

      // interpolate in 'r'
      for(int b = 0; b < p_cubNq; ++b; @inner(1))
        for(int i = 0; i < p_cubNq; ++i; @inner(0))
          if(b < p_Nq) {
            dfloat U1  = 0, V1 = 0,  W1 = 0;
            for(int a = 0; a < p_Nq; ++a) {
              dfloat Iia = s_cubInterpT[a][i];
              U1  += Iia * s_U[b][a];
              V1  += Iia * s_V[b][a];
              W1  += Iia * s_W[b][a];
            }

            s_U1[b][i] = U1;
            s_V1[b][i] = V1;
            s_W1[b][i] = W1;
          }

      @barrier("local");

      // interpolate in 's'
      for(int j = 0; j < p_cubNq; ++j; @inner(1))
        for(int i = 0; i < p_cubNq; ++i; @inner(0)) {
          dfloat U2 = 0, V2 = 0,  W2 = 0;
          // interpolate in b
          for(int b = 0; b < p_Nq; ++b) {
            dfloat Ijb = s_cubInterpT[b][j];
            U2 += Ijb * s_U1[b][i];
            V2 += Ijb * s_V1[b][i];
            W2 += Ijb * s_W1[b][i];
          }

          // interpolate in c progressively
          for(int k = 0; k < p_cubNq; ++k) {
            dfloat Ikc = s_cubInterpT[c][k];
            r_U[k] += Ikc * U2;
            r_V[k] += Ikc * V2;
            r_W[k] += Ikc * W2;
          }

          for(int k = 0; k < p_cubNq; ++k) {
            r_Ud[k] = r_U[k];
            r_Vd[k] = r_V[k];
            r_Wd[k] = r_W[k];
          }
        }
    }

    for(int k = 0; k < p_cubNq; ++k) {
      @barrier("local");

      for(int j = 0; j < p_cubNq; ++j; @inner(1))
        for(int i = 0; i < p_cubNq; ++i; @inner(0)) {
          s_U1[j][i] = r_Ud[k];
          s_V1[j][i] = r_Vd[k];
          s_W1[j][i] = r_Wd[k];
        }

      @barrier("local");

      for(int j = 0; j < p_cubNq; ++j; @inner(1))
        for(int i = 0; i < p_cubNq; ++i; @inner(0)) {
          dfloat Udr = 0, Uds = 0, Udt = 0;
          dfloat Vdr = 0, Vds = 0, Vdt = 0;
          dfloat Wdr = 0, Wds = 0, Wdt = 0;

          for(int n = 0; n < p_cubNq; ++n) {
            dfloat Din = s_cubD[i][n];
            Udr += Din * s_U1[j][n];
            Vdr += Din * s_V1[j][n];
            Wdr += Din * s_W1[j][n];
          }

          for(int n = 0; n < p_cubNq; ++n) {
            dfloat Djn = s_cubD[j][n];
            Uds += Djn * s_U1[n][i];
            Vds += Djn * s_V1[n][i];
            Wds += Djn * s_W1[n][i];
          }

          for(int n = 0; n < p_cubNq; ++n) {
            dfloat Dkn = s_cubD[k][n];
            Udt += Dkn * r_Ud[n];
            Vdt += Dkn * r_Vd[n];
            Wdt += Dkn * r_Wd[n];
          }

          const int id = e * p_cubNp + k * p_cubNq * p_cubNq + j * p_cubNq + i;
          const dfloat Uhat = Urst[id + 0 * cubatureOffset];
          const dfloat Vhat = Urst[id + 1 * cubatureOffset];
          const dfloat What = Urst[id + 2 * cubatureOffset];

          // I_f^t*(J_f*C_f^t)*G_f*\hat{D}_f*I_f*u
          r_U[k] = Uhat * Udr + Vhat * Uds + What * Udt;
          r_V[k] = Uhat * Vdr + Vhat * Vds + What * Vdt;
          r_W[k] = Uhat * Wdr + Vhat * Wds + What * Wdt;
        }
    }

    // now project back in t
    for(int c = 0; c < p_Nq; ++c) {
      @barrier("local");

      for(int j = 0; j < p_cubNq; ++j; @inner(1))
        for(int i = 0; i < p_cubNq; ++i; @inner(0)) {
          dfloat rhsU = 0, rhsV = 0, rhsW = 0;

          for(int k = 0; k < p_cubNq; ++k) {
            dfloat Ikc = s_cubInterpT[c][k];
            rhsU += Ikc * r_U[k];
            rhsV += Ikc * r_V[k];
            rhsW += Ikc * r_W[k];
          }

          s_U[j][i] = rhsU;
          s_V[j][i] = rhsV;
          s_W[j][i] = rhsW;
        }

      @barrier("local");

      for(int b = 0; b < p_cubNq; ++b; @inner(1))
        for(int i = 0; i < p_cubNq; ++i; @inner(0))
          if(b < p_Nq) {
            dfloat rhsU = 0, rhsV = 0, rhsW = 0;

            for(int j = 0; j < p_cubNq; ++j) {
              dfloat Ijb = s_cubInterpT[b][j];
              rhsU += Ijb * s_U[j][i];
              rhsV += Ijb * s_V[j][i];
              rhsW += Ijb * s_W[j][i];
            }

            s_U1[b][i] = rhsU;
            s_V1[b][i] = rhsV;
            s_W1[b][i] = rhsW;
          }

      @barrier("local");

      for(int b = 0; b < p_cubNq; ++b; @inner(1))
        for(int a = 0; a < p_cubNq; ++a; @inner(0))
          if(a < p_Nq && b < p_Nq) {
            dfloat rhsU = 0, rhsV = 0, rhsW = 0;

            for(int i = 0; i < p_cubNq; ++i) {
              dfloat Iia = s_cubInterpT[a][i];
              rhsU += Iia * s_U1[b][i];
              rhsV += Iia * s_V1[b][i];
              rhsW += Iia * s_W1[b][i];
            }
            const dlong gid = e * p_Np * p_Nvgeo + c * p_Nq * p_Nq + b * p_Nq + a;
            const dfloat IJW   = vgeo[gid + p_IJWID * p_Np];

            const dlong id = e * p_Np + c * p_Nq * p_Nq + b * p_Nq + a;
            NU[id + 0 * offset] = IJW * rhsU;
            NU[id + 1 * offset] = IJW * rhsV;
            NU[id + 2 * offset] = IJW * rhsW;
          }
    }
  }
}
