/*
   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */

// adding dp/dn -n\dot F
// if slip (dp/dn == 0) -n\dot F else -g0/dt n \dot u^(n+1)
#define surfaceTerms(sk, face, m, i, j)                                                                      \
  {                                                                                                          \
    struct bcData bc;                                                                                        \
    const dlong idM = vmapM[sk];                                                                             \
                                                                                                             \
    bc.nx = sgeo[sk * p_Nsgeo + p_NXID];                                                                     \
    bc.ny = sgeo[sk * p_Nsgeo + p_NYID];                                                                     \
    bc.nz = sgeo[sk * p_Nsgeo + p_NZID];                                                                     \
    bc.u = U[idM + 0 * offset];                                                                              \
    bc.v = U[idM + 1 * offset];                                                                              \
    bc.w = U[idM + 2 * offset];                                                                              \
    const dfloat WSJ = sgeo[sk * p_Nsgeo + p_WSJID];                                                         \
    const dlong bcType = EToB[face + p_Nfaces * e];                                                          \
    dfloat flux = 0;                                                                                         \
    if (bcType == p_bcTypeV || bcType == p_bcTypeINT) {                                                      \
      flux = coef * (bc.nx * bc.u + bc.ny * bc.v + bc.nz * bc.w);                                            \
    }                                                                                                        \
    else if (bcType == p_bcTypeSYMX || bcType == p_bcTypeSYMY ||                                             \
             bcType == p_bcTypeSYMZ || bcType == p_bcTypeSYM) {                                              \
      const dfloat fx = F[idM + 0 * offset];                                                                 \
      const dfloat fy = F[idM + 1 * offset];                                                                 \
      const dfloat fz = F[idM + 2 * offset];                                                                 \
      flux = bc.nx * fx + bc.ny * fy + bc.nz * fz;                                                           \
    }                                                                                                        \
    s_fluxDiv[m][j][i] = -WSJ * flux;                                                                        \
  }

@kernel void divergenceSurfaceHex3D(const dlong Nelements,
                                    @ restrict const dfloat *sgeo,
                                    @ restrict const dlong *vmapM,
                                    @ restrict const int *EToB,
                                    const dfloat coef,
                                    const dlong offset,
                                    @ restrict const dfloat *F,
                                    @ restrict const dfloat *U,
                                    @ restrict dfloat *divU)
{
  // for all elements
  for (dlong e = 0; e < Nelements; e++; @outer(0)) {
    // @shared storage for flux terms
    @shared dfloat s_fluxDiv[2][p_Nq][p_Nq];

    @exclusive dfloat r_div[p_Nq];

    // for all face nodes of all elements
    // face 0 & 5
    for (int j = 0; j < p_Nq; ++j; @inner(1)) {
      for (int i = 0; i < p_Nq; ++i; @inner(0)) {
#pragma unroll p_Nq
        for (int k = 0; k < p_Nq; ++k)
          r_div[k] = 0.;

        const dlong sk0 = e * p_Nfp * p_Nfaces + 0 * p_Nfp + i + j * p_Nq;
        const dlong sk5 = e * p_Nfp * p_Nfaces + 5 * p_Nfp + i + j * p_Nq;

        surfaceTerms(sk0, 0, 0, i, j);
        surfaceTerms(sk5, 5, 1, i, j);
      }
    }

    @barrier();

    // face 0 & 5
    for (int j = 0; j < p_Nq; ++j; @inner(1))
      for (int i = 0; i < p_Nq; ++i; @inner(0)) {
        // face 0
        r_div[0] += s_fluxDiv[0][j][i];

        // face 5
        r_div[p_Nq - 1] += s_fluxDiv[1][j][i];
      }

    @barrier();

    // face 1 & 3
    for (int k = 0; k < p_Nq; ++k; @inner(1))
      for (int i = 0; i < p_Nq; ++i; @inner(0)) {
        const dlong sk1 = e * p_Nfp * p_Nfaces + 1 * p_Nfp + i + k * p_Nq;
        const dlong sk3 = e * p_Nfp * p_Nfaces + 3 * p_Nfp + i + k * p_Nq;

        surfaceTerms(sk1, 1, 0, i, k);
        surfaceTerms(sk3, 3, 1, i, k);
      }

    @barrier();

    // face 1 & 3
    for (int j = 0; j < p_Nq; ++j; @inner(1)) {
      for (int i = 0; i < p_Nq; ++i; @inner(0)) {
        if (j == 0) { // face 1
#pragma unroll p_Nq
          for (int k = 0; k < p_Nq; k++)
            r_div[k] += s_fluxDiv[0][k][i];
        }
        if (j == p_Nq - 1) { // face 3
#pragma unroll p_Nq
          for (int k = 0; k < p_Nq; k++)
            r_div[k] += s_fluxDiv[1][k][i];
        }
      }
    }

    @barrier();

    // face 2 & 4
    for (int k = 0; k < p_Nq; ++k; @inner(1))
      for (int j = 0; j < p_Nq; ++j; @inner(0)) {
        const dlong sk2 = e * p_Nfp * p_Nfaces + 2 * p_Nfp + j + k * p_Nq;
        const dlong sk4 = e * p_Nfp * p_Nfaces + 4 * p_Nfp + j + k * p_Nq;

        surfaceTerms(sk2, 2, 0, j, k);
        surfaceTerms(sk4, 4, 1, j, k);
      }

    @barrier();

    // face 2 & 4
    for (int j = 0; j < p_Nq; ++j; @inner(1)) {
      for (int i = 0; i < p_Nq; ++i; @inner(0)) {
        if (i == p_Nq - 1) { // face 2
#pragma unroll p_Nq
          for (int k = 0; k < p_Nq; k++)
            r_div[k] += s_fluxDiv[0][k][j];
        }
        if (i == 0) { // face 4
#pragma unroll p_Nq
          for (int k = 0; k < p_Nq; k++)
            r_div[k] += s_fluxDiv[1][k][j];
        }
      }
    }

    @barrier();

    for (int j = 0; j < p_Nq; ++j; @inner(1)) {
      for (int i = 0; i < p_Nq; ++i; @inner(0)) {
#pragma unroll p_Nq
        for (int k = 0; k < p_Nq; ++k) {
          const dlong id = e * p_Np + k * p_Nq * p_Nq + j * p_Nq + i;

          divU[id] += r_div[k];
        }
      }
    }
  }
}

#undef surfaceTerms
