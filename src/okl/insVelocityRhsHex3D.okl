/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

// rhsU^s = MM*(U^n - \sum^s-1 ea_si N(U^i) + \sum^s-1 ia_si LU^i - \sum^s-1 pa_si GP^i)/ia_ss nu dt
@kernel void insVelocityRhsTOMBOHex3D(const dlong Nelements,
                                      @restrict const  dfloat *  vgeo,
                                      @restrict const  dfloat *  MM,
                                      const dfloat idt,
                                      const dfloat inu,
                                      const dfloat g0,
                                      @restrict const  dfloat *  extbdfA,
                                      @restrict const  dfloat *  extbdfB,
                                      const dlong fieldOffset,
                                      @restrict const  dfloat *  U,
                                      @restrict const  dfloat *  NU,
                                      @restrict const  dfloat *  FU,
                                      @restrict const  dfloat *  GP,
                                            @restrict dfloat *  rhsU,
                                            @restrict dfloat *  rhsV,
                                            @restrict dfloat *  rhsW){
  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){

    for(int es=0;es<p_NblockV;++es;@inner(2)){// for all elements in block
      for(int j=0;j<p_Nq;++j;@inner(1)){ 
        for(int i=0;i<p_Nq;++i;@inner(0)){
          const dlong e = eo+es; // element in block
          if(e<Nelements){
            #pragma unroll p_Nq
            for(int k=0;k<p_Nq;++k){
              const dlong id = i + j*p_Nq + k*p_Nq*p_Nq + p_Np*e;
              const dlong gid = i + j*p_Nq + k*p_Nq*p_Nq + e*p_Np*p_Nvgeo;
              const dfloat JW = vgeo[gid + p_JWID*p_Np];

              dfloat rhsUn;
              dfloat rhsVn;
              dfloat rhsWn;

              if (p_SUBCYCLING) {
              // Gradient P
              rhsUn = -JW*inu*GP[id+0*fieldOffset];
              rhsVn = -JW*inu*GP[id+1*fieldOffset];
              rhsWn = -JW*inu*GP[id+2*fieldOffset];
              // Characteristic Velocity
              rhsUn += JW*inu*idt*NU[id+0*fieldOffset];
              rhsVn += JW*inu*idt*NU[id+1*fieldOffset];
              rhsWn += JW*inu*idt*NU[id+2*fieldOffset];

                for (int s=0;s<p_Nstages;s++) {
                  // FU
                  const dfloat FUx  = FU[id+0*fieldOffset+s*p_NVfields*fieldOffset];
                  const dfloat FUy  = FU[id+1*fieldOffset+s*p_NVfields*fieldOffset];
                  const dfloat FUz  = FU[id+2*fieldOffset+s*p_NVfields*fieldOffset];
                  // 
                  rhsUn += JW*inu*extbdfA[s]*FUx;
                  rhsVn += JW*inu*extbdfA[s]*FUy;
                  rhsWn += JW*inu*extbdfA[s]*FUz;
                }
              } else {
              rhsUn = -JW*inu*GP[id+0*fieldOffset];
              rhsVn = -JW*inu*GP[id+1*fieldOffset];
              rhsWn = -JW*inu*GP[id+2*fieldOffset];

                for (int s=0;s<p_Nstages;s++) {
                  //U 
                  const dfloat Um  = U[id+0*fieldOffset+s*p_NVfields*fieldOffset];
                  const dfloat Vm  = U[id+1*fieldOffset+s*p_NVfields*fieldOffset];
                  const dfloat Wm  = U[id+2*fieldOffset+s*p_NVfields*fieldOffset];
                  // NU
                  const dfloat NUm  = NU[id+0*fieldOffset+s*p_NVfields*fieldOffset];
                  const dfloat NVm  = NU[id+1*fieldOffset+s*p_NVfields*fieldOffset];
                  const dfloat NWm  = NU[id+2*fieldOffset+s*p_NVfields*fieldOffset];
                  // FU
                  const dfloat FUx  = FU[id+0*fieldOffset+s*p_NVfields*fieldOffset];
                  const dfloat FUy  = FU[id+1*fieldOffset+s*p_NVfields*fieldOffset];
                  const dfloat FUz  = FU[id+2*fieldOffset+s*p_NVfields*fieldOffset];


                  rhsUn += JW*inu*(idt*extbdfB[s]*Um - extbdfA[s]*(NUm - FUx));
                  rhsVn += JW*inu*(idt*extbdfB[s]*Vm - extbdfA[s]*(NVm - FUy));
                  rhsWn += JW*inu*(idt*extbdfB[s]*Wm - extbdfA[s]*(NWm - FUz));
                }
              }

              rhsU[id] = rhsUn;
              rhsV[id] = rhsVn;
              rhsW[id] = rhsWn;
            }
          }
        }
      }
    }
  }
}
