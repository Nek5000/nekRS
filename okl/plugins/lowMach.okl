/*

   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */

@kernel void qtlHex3D(const dlong Nelements,
                         @restrict const dfloat*  vgeo,
                         @restrict const dfloat*  D,
                         const dlong offset,
                         @restrict const dfloat*  gradQ,
                         @restrict const dfloat*  Q,
                         @restrict const dfloat*  DIFF,
                         @restrict const dfloat*  RHOCP,
                         @restrict const dfloat*  SRC,
                         @restrict dfloat*  QTL)
{
  for(dlong e = 0; e < Nelements; ++e; @outer(0)) {
    @shared dfloat s_U[p_Nq][p_Nq][p_Nq];
    @shared dfloat s_V[p_Nq][p_Nq][p_Nq];
    @shared dfloat s_W[p_Nq][p_Nq][p_Nq];

    @shared dfloat s_D[p_Nq][p_Nq];

    for(int k = 0; k < p_Nq; ++k; @inner(2))
      for(int j = 0; j < p_Nq; ++j; @inner(1))
        for(int i = 0; i < p_Nq; ++i; @inner(0)) {
          dlong id = i + j * p_Nq;
          if(k == 0)
            s_D[0][id] = D[id];

          id = e * p_Np + k * p_Nq * p_Nq + j * p_Nq + i;

          const dfloat Un = gradQ[id + 0 * offset];
          const dfloat Vn = gradQ[id + 1 * offset];
          const dfloat Wn = gradQ[id + 2 * offset];
          const dfloat cond = DIFF[id];

          s_U[k][j][i] = cond * Un;
          s_V[k][j][i] = cond * Vn;
          s_W[k][j][i] = cond * Wn;
        }

    @barrier("local");

    for(int k = 0; k < p_Nq; ++k; @inner(2)) {
      for(int j = 0; j < p_Nq; ++j; @inner(1)) {
        for(int i = 0; i < p_Nq; ++i; @inner(0)) {
          const dlong id    = e * p_Np        + k * p_Nq * p_Nq + j * p_Nq + i;
          const dlong gid   = e * p_Np * p_Nvgeo + k * p_Nq * p_Nq + j * p_Nq + i;
          const dfloat drdx = vgeo[gid + p_RXID * p_Np];
          const dfloat drdy = vgeo[gid + p_RYID * p_Np];
          const dfloat drdz = vgeo[gid + p_RZID * p_Np];
          const dfloat dsdx = vgeo[gid + p_SXID * p_Np];
          const dfloat dsdy = vgeo[gid + p_SYID * p_Np];
          const dfloat dsdz = vgeo[gid + p_SZID * p_Np];
          const dfloat dtdx = vgeo[gid + p_TXID * p_Np];
          const dfloat dtdy = vgeo[gid + p_TYID * p_Np];
          const dfloat dtdz = vgeo[gid + p_TZID * p_Np];
          const dfloat JW   = vgeo[gid + p_JWID * p_Np];

          const dfloat iRhoCpT = 1 / (RHOCP[id] * Q[id]);
          const dfloat qvol    = SRC[id];

          dfloat dudr = 0, duds = 0, dudt = 0;
          dfloat dvdr = 0, dvds = 0, dvdt = 0;
          dfloat dwdr = 0, dwds = 0, dwdt = 0;
          dfloat div = 0;
#pragma unroll p_Nq
          for (int n = 0; n < p_Nq; n++) {
            const dfloat Dr = s_D[i][n];
            const dfloat Ds = s_D[j][n];
            const dfloat Dt = s_D[k][n];
            dudr += Dr * s_U[k][j][n];
            duds += Ds * s_U[k][n][i];
            dudt += Dt * s_U[n][j][i];

            dvdr += Dr * s_V[k][j][n];
            dvds += Ds * s_V[k][n][i];
            dvdt += Dt * s_V[n][j][i];

            dwdr += Dr * s_W[k][j][n];
            dwds += Ds * s_W[k][n][i];
            dwdt += Dt * s_W[n][j][i];
          }

          div  = (drdx * dudr + dsdx * duds + dtdx * dudt);
          div += (drdy * dvdr + dsdy * dvds + dtdy * dvdt);
          div += (drdz * dwdr + dsdz * dwds + dtdz * dwdt);

          QTL[id] += JW * iRhoCpT * (div + qvol);
        }
      }
    }
  }
}
@kernel void p0thHelper(const dlong N,
                      const dfloat dd,
                      @restrict const dfloat* rhoCp,
                      @restrict const dfloat* rho,
                      @restrict const dfloat* massMatrix,
                      @restrict dfloat *  w1,
                      @restrict dfloat * w2)
{
  for(dlong n=0;n<N;++n;@tile(p_blockSize,@outer,@inner)){
    if(n<N){
      const dfloat w2_val = dd * rhoCp[n] / rho[n] + 1.0;
      w2[n] = w2_val;
      w1[n] = w2_val * massMatrix[n];
    }
  }
}
#define surfaceTerms(sk,face,m, i, j)                                                  \
  {                                                                                    \
    const dlong idM = vmapM[sk];                                                       \
    const dlong id = j * p_Nq + i;                                                     \
                                                                                       \
    const dfloat nx = sgeo[sk * p_Nsgeo + p_NXID];                                     \
    const dfloat ny = sgeo[sk * p_Nsgeo + p_NYID];                                     \
    const dfloat nz = sgeo[sk * p_Nsgeo + p_NZID];                                     \
    const dfloat u  = U[idM + 0 * offset];                                             \
    const dfloat v  = U[idM + 1 * offset];                                             \
    const dfloat w  = U[idM + 2 * offset];                                             \
    const dfloat WSJ = sgeo[sk * p_Nsgeo + p_WSJID];                                   \
    const dlong bcType = EToB[face + p_Nfaces * e];                                    \
    dfloat flux = 0;                                                                   \
    if(bcType == 2) {                                                                  \
      flux = (nx * u + ny * v + nz * w);                                               \
    }                                                                                  \
    s_flux[id] += WSJ*flux;                                                             \
  }


// WARNING: This kernel implicitly assumes that p_blockSize > p_Nq * p_Nq.
// If this is not the case, then the results produced by this kernel will be garbage.
@kernel void surfaceFlux(const dlong Nelements,
                                            @restrict const dfloat*  sgeo,
                                            @restrict const dlong*  vmapM,
                                            @restrict const int*  EToB,
                                            const dlong offset,
                                            @restrict const dfloat*  U,
                                            @restrict dfloat*  flux)
{
  // for all elements
  for(dlong e = 0; e < Nelements; e++; @outer(0)) {
    // @shared storage for flux terms
    @shared dfloat s_flux[p_blockSize];
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        for (int k = j*p_Nq + i; k < p_blockSize; k += p_Nq*p_Nq) s_flux[k] = 0.0;
      }
    }
    @barrier("local");

    // for all face nodes of all elements
    // face 0 & 5
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {

        const dlong sk0 = e * p_Nfp * p_Nfaces + 0 * p_Nfp + i + j * p_Nq;
        const dlong sk5 = e * p_Nfp * p_Nfaces + 5 * p_Nfp + i + j * p_Nq;

        surfaceTerms(sk0,0,0,i,j);
        surfaceTerms(sk5,5,1,i,j);
      }
    }

    @barrier("local");

    // face 1 & 3
    for(int k = 0; k < p_Nq; ++k; @inner(1))
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const dlong sk1 = e * p_Nfp * p_Nfaces + 1 * p_Nfp + i + k * p_Nq;
        const dlong sk3 = e * p_Nfp * p_Nfaces + 3 * p_Nfp + i + k * p_Nq;

        surfaceTerms(sk1,1,0,i,k);
        surfaceTerms(sk3,3,1,i,k);
      }

    @barrier("local");

    // face 2 & 4
    for(int k = 0; k < p_Nq; ++k; @inner(1))
      for(int j = 0; j < p_Nq; ++j; @inner(0)) {
        const dlong sk2 = e * p_Nfp * p_Nfaces + 2 * p_Nfp + j + k * p_Nq;
        const dlong sk4 = e * p_Nfp * p_Nfaces + 4 * p_Nfp + j + k * p_Nq;

        surfaceTerms(sk2,2,0,j,k);
        surfaceTerms(sk4,4,1,j,k);
      }

    @barrier("local");

    // reduce p_Nq * p_Nq to single value
    #if p_blockSize > 512
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 512){
          s_flux[t] += s_flux[t+512];
        }
      }
    }
    @barrier("local");
    #endif
    #if p_blockSize > 256
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 256){
          s_flux[t] += s_flux[t+256];
        }
      }
    }
    @barrier("local");
    #endif
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 128){
          s_flux[t] += s_flux[t+128];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 64){
          s_flux[t] += s_flux[t+64];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 32){
          s_flux[t] += s_flux[t+32];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 16){
          s_flux[t] += s_flux[t+16];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 8){
          s_flux[t] += s_flux[t+8];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 4){
          s_flux[t] += s_flux[t+4];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 2){
          s_flux[t] += s_flux[t+2];
        }
      }
    }
    @barrier("local");
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const int t = j * p_Nq + i;
        if(t < 1){
          flux[e] = s_flux[0] + s_flux[1];
        }
      }
    }
  }
}
