C-----------------------------------------------------------------------
C
C  This is closed domain with a moving postion.
C  The resulting 1D isentropic flow is compared to an analytical solution.  
C
c-----------------------------------------------------------------------
      SUBROUTINE USERVP (IX,IY,IZ,ieg)

C Set user variable properties

      INCLUDE 'SIZE'   
      INCLUDE 'TOTAL'  
      INCLUDE 'NEKUSE' 

      if (ifield.eq.1) then 
         utrans = 1.0*p0th/temp
         udiff  = 1e-2
      else
         utrans = 1.0*p0th/temp
         udiff  = 1e-2 
      endif

      RETURN
      END

C=======================================================================

      SUBROUTINE USERF  (IX,IY,IZ,ieg)                

C Set user forcing function for the momentum

      INCLUDE 'SIZE'   
      INCLUDE 'TSTEP'  
      INCLUDE 'NEKUSE' 

      FFY = 0.0
      FFX = 0.0
      FFZ = 0.0

      RETURN
      END

C=======================================================================

      SUBROUTINE USERQ  (IX,IY,IZ,ieg)                

C Set user forcing function for the energy and species

      INCLUDE 'SIZE'   
      INCLUDE 'TOTAL'  
      INCLUDE 'NEKUSE' 
      
      qvol = 0.0

      RETURN
      END

C=======================================================================

      SUBROUTINE USERCHK

      INCLUDE 'SIZE'
      INCLUDE 'TOTAL'

      !ifusermv = .true.                  ! define our own mesh velocity
      !call user_mvel(time)               ! Fill mesh velocity

      call user_print_err

      RETURN
      END

C=======================================================================

      SUBROUTINE USERBC (IX,IY,IZ,ISIDE,ieg)                

      INCLUDE 'SIZE'
      INCLUDE 'TSTEP'
      INCLUDE 'NEKUSE'
      include 'CASEDATA'

      real uxt, uyt

      omega = P_OMEGA
      amp   = P_AMP

      IF (IFIELD .eq. 1) THEN 
        uxt = 0.0
        uyt = amp * sin(omega * time)

        UX = uxt * cos(P_ROT) - uyt * sin(P_ROT)
        UY = uxt * sin(P_ROT) + uyt * cos(P_ROT)
        UZ = 0.0

      ENDIF

      return
      END   

C=======================================================================

      SUBROUTINE USERIC (IX,IY,IZ,ieg)

C   Set initial conditions

      INCLUDE 'SIZE'
      INCLUDE 'TOTAL'
      INCLUDE 'NEKUSE'

      p0th = 1.0

      IF (IFIELD.EQ.1) THEN          ! ---------- Velocity
          UX = 0.0
          UY = 0.0
          UZ = 0.0
      ENDIF

      IF (IFIELD.EQ.2)               ! ---------- Temperature
     &    TEMP = 1.0 

      RETURN
      END

C=======================================================================

      subroutine usrdat

      return
      end

C=======================================================================
      subroutine usrdat2()  ! This routine to modify mesh coordinates

      include 'SIZE'
      include 'TOTAL'
      include 'CASEDATA'
      
      common /scnrs/ sc_nrs(10)
      real                sc_nrs

      XLEN = sc_nrs(1)
      YLEN = sc_nrs(2)
      ZLEN = sc_nrs(3)

      P_GAMMA = sc_nrs(4)
      P_DELTA = sc_nrs(5)
      P_OMEGA = sc_nrs(6)
      P_AMP = sc_nrs(7)
      P_ROT = sc_nrs(8)

      call rescale_x(xm1, 0.0,XLEN)
      call rescale_x(ym1,-1.0,-1.0+YLEN)
      call rescale_x(zm1, 0.0,ZLEN)

      ! rotate coordinates
      ntot = nx1*ny1*nz1*nelt
      do i=1,ntot
        xpt = xm1(i,1,1,1)
        ypt = ym1(i,1,1,1)

        xm1(i,1,1,1) = xpt * cos(P_ROT) - ypt * sin(P_ROT)
        ym1(i,1,1,1) = xpt * sin(P_ROT) + ypt * cos(P_ROT)
      enddo


      do iel=1,nelt
      do ifc=5,6
         cbc(ifc,iel,1) = 'P  '
      enddo
      enddo

      do iel=1,nelt
      do ifc=1,2*ndim
         if (cbc(ifc,iel,1) .eq. 'mv ') then
           boundaryID(ifc,iel) = 1
         endif
         if (cbc(ifc,iel,1) .eq. 'W  ') then
           boundaryID(ifc,iel) = 2
         endif
         if (cbc(ifc,iel,1) .eq. 'SYM') then
           boundaryID(ifc,iel) = 3
         endif
      enddo
      enddo

      return
      end

c-----------------------------------------------------------------------

      subroutine usrsetvert(glo_num,nel,nx,ny,nz) ! to modify glo_num
      integer*8 glo_num(1)

      ! kludge for periodic bc in z
      nxy  = nx*ny
      nxyz = nx*ny*nz
      do iel = 1,nel
         ioff = nxyz*(iel-1)
         do ixy = 1,nxy
            glo_num(ioff + nxy*(nz-1) + ixy) = glo_num(ioff + ixy)
         enddo
      enddo

      return
      end
C=======================================================================

      subroutine usrdat3

      return
      end

C=======================================================================
      subroutine user_mvel (time_)

      include 'SIZE'
      include 'TOTAL'
      include 'CASEDATA'

      real ht(lx1, ly1, lz1, lelt)
      real xp, yp

      real hmin, hmax, hlen, h, hsc
      real wxt, wyt

      common /c_mybc/ omega, v_piston

      n    = nx1*ny1*nz1*nelv
      do i=1,n
        xp = xm1(i,1,1,1)
        yp = ym1(i,1,1,1)
        ht(i,1,1,1) = -xp * sin(P_ROT) + yp * cos(P_ROT)
      enddo

      hmin = glmin(ht,n)
      hmax = glmax(ht,n)  ! this is the separation between moving and nonmvg
      hlen = hmin-hmax

      omega = P_OMEGA
      amp   = P_AMP
      v_piston = amp*sin(omega*time_)

      do i=1,n
        h = ht(i,1,1,1)
        hsc = (h-hmax)/hlen
        wxt = 0.0
        wyt = hsc * v_piston

        wx(i,1,1,1) = wxt * cos(P_ROT) - wyt * sin(P_ROT)
        wy(i,1,1,1) = wxt * sin(P_ROT) + wyt * cos(P_ROT)
        if (if3d) wz(i,1,1,1) = 0
      enddo

      return
      end
C-----------------------------------------------------------------------
      subroutine exact_sol (time_, volex, vpex,pex,dpdtex,qtlex,ypex)
      include 'CASEDATA'

      gam0   = P_GAMMA
      omega  = P_OMEGA
      amp    = P_AMP
      areap  = XLEN*ZLEN 
      volex0 = areap * YLEN 
      pex0   = 1.0

      vpex  = amp*sin(omega*time_)
      volex = volex0 + areap * amp * (cos(omega*time_) - 1.0) / omega
      pex   = pex0 *(volex0/volex)**gam0
      dpdtex= gam0 * pex0 * volex0**gam0 * areap*vpex/volex**(gam0+1.0)
      qtlex =( (gam0-1.0)/gam0 - 1.0 ) * dpdtex/pex
      ypex  =-0.5*( 1.0 + cos(omega*time_) )

      return
      end
c =======================================================================
      subroutine user_print_err
c
      include 'SIZE'
      include 'TOTAL'
      include 'CASEDATA'

      real glcflux
      real tavg, tex, var
      real err(6)
      save err
      real ht(lx1, ly1, lz1, lelt)
      COMMON /NRSSCPTR/ nrs_scptr(1)
      integer*8         nrs_scptr

      nrs_scptr(1) = loc(err)

      nxyz  = nx1*ny1*nz1
      ntotv = nxyz*nelv

      termV = -1.*glcflux(vx,vy,vz)
      do i = 1,ntotv
        xp = xm1(i,1,1,1)
        yp = ym1(i,1,1,1)
        ht(i,1,1,1) = -xp * sin(P_ROT) + yp * cos(P_ROT)
      enddo
      ypist = glmin(ht,ntotv)

      call exact_sol (time,volex,vpex,pex,dpdtex,qtlex,ypex)
      tavg = glsc2(t, bm1, ntotv) / volvm1
      tex = pex ** ((P_GAMMA-1.)/P_GAMMA)

      if(nid.eq.0) write(*,'(A,7(1X,E15.7))') 'err',time,
     $   volex-volvm1,(vpex*XLEN*ZLEN)-termV,
     &   pex-p0th,dpdtex-dp0thdt,ypex-ypist,
     &   tavg - tex
      err(1) = abs(volex - volvm1)
      err(2) = abs(vpex*XLEN*ZLEN-termV)
      err(3) = abs(pex-p0th)
      err(4) = abs(dpdtex-dp0thdt)
      err(5) = abs(ypex-ypist)
      err(6) = abs(tavg-tex)

      return
      end

