/*

   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */

@kernel void scalarMultiply(const dlong N,
                            const dlong fieldOffset,
                            const dlong offset,
                            const dfloat alpha,
                            @restrict dfloat*  x)
{
  for(dlong n = 0; n < N; ++n; @tile(p_blockSize,@outer,@inner))
    if(n < N){
      #pragma unroll p_Nfields
      for(dlong field = 0 ; field < p_Nfields; ++field){
        x[n + offset + field * fieldOffset] = alpha * x[n + offset + field * fieldOffset];
      }
    }
}

@kernel void multiScaledAddwOffset(const dlong N,
                                   const dlong m,
                                   const dlong destOffset,
                                   const dlong fieldOffset,
                                   @restrict const dfloat* alphas,
                                   const dfloat beta,
                                   @restrict dfloat*  x)
{
  for(dlong n = 0; n < N; ++n; @tile(p_blockSize,@outer,@inner))
    if(n < N){
      for(dlong k = 0; k < m - 1; ++k)
        for(dlong fld = 0; fld < p_Nfields; ++fld)
          x[n + destOffset + fld * fieldOffset] = -alphas[k] * x[n + p_Nfields * k * fieldOffset + fld * fieldOffset] + beta * x[n + destOffset + fld * fieldOffset];
    }
}

@kernel void accumulate(const dlong N,
                        const dlong m,
                        const dlong fieldOffset,
                        @restrict const dfloat* alpha,
                        @restrict const dfloat*  x,
                        @restrict dfloat* y)
{
  for(dlong n = 0; n < N; ++n; @tile(p_blockSize,@outer,@inner))
    if(n < N) {
      // y = alpha[0] * o_xx[:,0]
      for(dlong fld = 0 ; fld < p_Nfields; ++fld)
        y[n + fld * fieldOffset] = alpha[0] * x[n + fld * fieldOffset];
      for(dlong k = 1; k < m; ++k)
        for(dlong fld = 0 ; fld < p_Nfields; ++fld)
          // y += alpha[k] * o_xx[:,k]
          y[n + fld * fieldOffset] += alpha[k] * x[n + p_Nfields * k * fieldOffset + fld * fieldOffset];
    }
}

@kernel void multiWeightedInnerProduct2(const dlong N,
                                        const dlong fieldOffset,
                                        const dlong Nblock,
                                        const dlong m,
                                        const dlong offset,
                                        @restrict const dfloat*  w,
                                        @restrict const dfloat*  x,
                                        @restrict const dfloat*  y,
                                        @restrict dfloat*  wxy)
{
  for(dlong b = 0; b < Nblock; ++b; @outer(0)) {
    @shared volatile dfloat s_wxy[p_blockSize];
    @exclusive dfloat tmp[p_Nfields];

    for(int t = 0; t < p_blockSize; ++t; @inner(0)) {
      const dlong id = t + p_blockSize * b;
      if(id < N) {
        #pragma unroll
        for(dlong fld = 0; fld < p_Nfields; ++fld)
          tmp[fld] = w[id] * y[id + offset + fld * fieldOffset];
      }
    }
    @barrier("local");

    for(int v = 0; v < m; ++v) {
      for(int t = 0; t < p_blockSize; ++t; @inner(0)) {
        const dlong id = t + p_blockSize * b;
        dfloat res = 0.0; 
        if(id < N) {
          #pragma unroll
          for(dlong fld = 0; fld < p_Nfields; ++fld)
            res += x[id + p_Nfields * fieldOffset * v + fld * fieldOffset] * tmp[fld];
        }
        s_wxy[t] = res;
      }
      @barrier("local");

#if p_blockSize > 512
      for(int t = 0; t < p_blockSize; ++t; @inner(0)) if(t < 512) s_wxy[t] += s_wxy[t + 512];
      @barrier("local");
#endif
#if p_blockSize > 256
      for(int t = 0; t < p_blockSize; ++t; @inner(0)) if(t < 256) s_wxy[t] += s_wxy[t + 256];
      @barrier("local");
#endif

      for(int t = 0; t < p_blockSize; ++t; @inner(0)) if(t < 128) s_wxy[t] += s_wxy[t + 128];
      @barrier("local");

      for(int t = 0; t < p_blockSize; ++t; @inner(0)) if(t < 64) s_wxy[t] += s_wxy[t + 64];
      @barrier("local");

      for(int t = 0; t < p_blockSize; ++t; @inner(0)) if(t < 32) s_wxy[t] += s_wxy[t + 32];
      for(int t = 0; t < p_blockSize; ++t; @inner(0)) if(t < 16) s_wxy[t] += s_wxy[t + 16];
      for(int t = 0; t < p_blockSize; ++t; @inner(0)) if(t <  8) s_wxy[t] += s_wxy[t + 8];
      for(int t = 0; t < p_blockSize; ++t; @inner(0)) if(t <  4) s_wxy[t] += s_wxy[t + 4];
      for(int t = 0; t < p_blockSize; ++t; @inner(0)) if(t <  2) s_wxy[t] += s_wxy[t + 2];

      for(int t = 0; t < p_blockSize; ++t; @inner(0)) if(t <  1) wxy[b + v * Nblock] = s_wxy[0] + s_wxy[1];
    }
  }
}
