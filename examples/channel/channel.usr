C
C  USER SPECIFIED ROUTINES: 
C
C     - boundary conditions 
C     - initial conditions  
C     - variable properties 
C     - forcing function for fluid (f)
C     - forcing function for passive scalar (q)
C     - general purpose routine for checking errors etc.        
C
c-----------------------------------------------------------------------
      subroutine useric (ix,iy,iz,ieg)
      include 'SIZE'
      include 'TOTAL'
      include 'NEKUSE'
      include 'CASEDATA'

      common /exavel/ uxex (lx1,ly1,lz1,lelv),uyex (lx1,ly1,lz1,lelv)
     $               ,uzex (lx1,ly1,lz1,lelv)

      ie = gllel(ieg)

      ux = uxex(ix,iy,iz,ie)*cos(P_ROT) - uyex(ix,iy,iz,ie)*sin(P_ROT)
      uy = uxex(ix,iy,iz,ie)*sin(P_ROT) + uyex(ix,iy,iz,ie)*cos(P_ROT)
      uz = uzex(ix,iy,iz,ie)

      return
      end
c-----------------------------------------------------------------------
      subroutine userchk
      include 'SIZE'
      include 'TOTAL'
      include 'CASEDATA'

      common /cforce/ forcx(lx1,ly1,lz1,lelv),forcy(lx1,ly1,lz1,lelv)
     $               ,forcz(lx1,ly1,lz1,lelv)

      common /exavel/ uxex (lx1,ly1,lz1,lelv),uyex (lx1,ly1,lz1,lelv)
     $               ,uzex (lx1,ly1,lz1,lelv)

      common /viscmol/mul   (lx1,ly1,lz1,lelv), mul_dx(lx1,ly1,lz1,lelv)
     $               ,mul_dy(lx1,ly1,lz1,lelv), mul_dz(lx1,ly1,lz1,lelv)
      real mul, mul_dx, mul_dy, mul_dz

      common /scrns/ dv1  (lx1,ly1,lz1,lelv),
     $               diffx(lx1,ly1,lz1,lelv),
     $               diffy(lx1,ly1,lz1,lelv)

      COMMON /NRSSCPTR/ nrs_scptr(5)
      integer*8         nrs_scptr

      real err(2)
      save err

      n = nx1*ny1*nz1*nelv

      nrs_scptr(1) = loc(mul(1,1,1,1))
      nrs_scptr(2) = loc(forcx(1,1,1,1))
      nrs_scptr(3) = loc(forcy(1,1,1,1))
      nrs_scptr(4) = loc(forcz(1,1,1,1))
      nrs_scptr(5) = loc(err(1))

      do i=1,n
        vxr = vx(i,1,1,1) * cos(P_ROT) + vy(i,1,1,1) * sin(P_ROT)
        vyr =-vx(i,1,1,1) * sin(P_ROT) + vy(i,1,1,1) * cos(P_ROT)
        diffx(i,1,1,1) = abs(vxr        -uxex(i,1,1,1))
        if (uxex(i,1,1,1).gt.1e-14) diffx(i,1,1,1) =
     $    diffx(i,1,1,1)/uxex(i,1,1,1)
        diffy(i,1,1,1) = abs(vyr        -uyex(i,1,1,1))
        if (uyex(i,1,1,1).gt.1e-14) diffy(i,1,1,1) =
     $    diffy(i,1,1,1)/uyex(i,1,1,1)
      enddo

      err(1) = glmax(diffx,n)
      err(2) = glmax(diffy,n)

      if (nio.eq.0)
     $   write(6,'(i6, 1p2e13.4,A)') istep, err(1),err(2),' Linf VX VY'

      !if(ifoutfld) then
      !   call outpost2(diffx,diffy,diffz,mul,t,0,'dif')
      !   call outpost2(mul_dx,mul_dy,mul_dz,mul,t,0,'mul')
      !   call outpost2(forcx,forcy,forcz,mul,t,0,'for')
      !   call outpost2(uxex,uyex,uzex,mul,t,0,'exa')
      !endif

      return
      end
c-----------------------------------------------------------------------
      subroutine usrdat   ! This routine to modify element vertices
      include 'SIZE'      ! _before_ mesh is generated, which 
      include 'TOTAL'     ! guarantees GLL mapping of mesh.
      integer e

      do e=1,nelv    !  Rescale mesh to [0,2]^2 x [0,0.1]
      do i=1,2**ndim !  Assumes original domain in .re2 file on [0,1]^3
         xc(i,e) = 2.0*xc(i,e)
         yc(i,e) = 2.0*yc(i,e)
         zc(i,e) = 0.1*zc(i,e)
      enddo
      enddo

      return
      end
c-----------------------------------------------------------------------
      subroutine usrdat2()  ! This routine to modify mesh coordinates

      include 'SIZE'
      include 'TOTAL'
      include 'CASEDATA'

      common /scnrs/ sc_nrs(10)
      real               sc_nrs

      P_ROT = sc_nrs(1)

      do iel=1,nelt
      do ifc=5,6
         cbc(ifc,iel,1) = 'P  '
      enddo
      do ifc=1,2*ndim
         if(cbc(ifc,iel,1) .eq. 'SYM' ) then
           boundaryID(ifc, iel) = 1
         endif
      enddo
      enddo

      call get_mol_visc
      call get_forcing
      call get_analsol

      ntot = nx1*ny1*nz1*nelt
      do i=1,ntot
        xpt = xm1(i,1,1,1)
        ypt = ym1(i,1,1,1)

        xm1(i,1,1,1) = xpt * cos(P_ROT) - ypt * sin(P_ROT)
        ym1(i,1,1,1) = xpt * sin(P_ROT) + ypt * cos(P_ROT)
      enddo


      return
      end
c-----------------------------------------------------------------------
      subroutine usrsetvert(glo_num,nel,nx,ny,nz) ! to modify glo_num
      integer*8 glo_num(1)

      ! kludge for periodic bc in z
      nxy  = nx*ny
      nxyz = nx*ny*nz
      do iel = 1,nel
         ioff = nxyz*(iel-1)
         do ixy = 1,nxy
            glo_num(ioff + nxy*(nz-1) + ixy) = glo_num(ioff + ixy)
         enddo
      enddo

      return
      end
c-----------------------------------------------------------------------
      subroutine usrdat3
      include 'SIZE'
      include 'TOTAL'

      return
      end
C-----------------------------------------------------------------------
      subroutine get_mol_visc
      include 'SIZE'
      include 'TOTAL'

      common /viscmol/mul   (lx1,ly1,lz1,lelv), mul_dx(lx1,ly1,lz1,lelv)
     $               ,mul_dy(lx1,ly1,lz1,lelv), mul_dz(lx1,ly1,lz1,lelv)
      real mul, mul_dx, mul_dy, mul_dz

      Re   = 1./param(2)
      pi   = 4.0*atan(1.0)
      ampl = 1.0

      do ie=1,nelv
      do iz=1,nz1
      do iy=1,ny1
      do ix=1,nx1
        x=xm1(ix,iy,iz,ie)
        y=ym1(ix,iy,iz,ie)
        z=zm1(ix,iy,iz,ie)
        pert_mul = y
        mul(ix,iy,iz,ie)=(1./Re)*(1. + ampl*pert_mul)
      enddo
      enddo
      enddo
      enddo

      call opgrad  (mul_dx,mul_dy,mul_dz,mul)
      call opdssum (mul_dx,mul_dy,mul_dz)
      call opcolv  (mul_dx,mul_dy,mul_dz,binvm1)

      return
      end
c-----------------------------------------------------------------------
      subroutine get_analsol

      include 'SIZE'
      include 'TOTAL'

      common /exavel/ uxex (lx1,ly1,lz1,lelv),uyex (lx1,ly1,lz1,lelv)
     $               ,uzex (lx1,ly1,lz1,lelv)

      n=nx1*ny1*nz1*nelv

      U0 = 1.
      pi = 4.0*atan(1.0)
      phx= pi/2.
      phy= pi/2.

      do i=1,n
        x = xm1(i,1,1,1)
        y = ym1(i,1,1,1)
        z = zm1(i,1,1,1)
        uxex (i,1,1,1) =     U0 * cos(pi*x+phx) * sin(2.*pi*y+phy)
        uyex (i,1,1,1) =-0.5*U0 * sin(pi*x+phx) * cos(2.*pi*y+phy)
        uzex (i,1,1,1) = 0.0*U0
      enddo

      return
      end

c-----------------------------------------------------------------------
      subroutine get_forcing

      include 'SIZE'
      include 'TOTAL'
      include 'CASEDATA'

      common /cforce/ forcx(lx1,ly1,lz1,lelv),forcy(lx1,ly1,lz1,lelv)
     $               ,forcz(lx1,ly1,lz1,lelv)
      common /viscmol/mul   (lx1,ly1,lz1,lelv), mul_dx(lx1,ly1,lz1,lelv)
     $               ,mul_dy(lx1,ly1,lz1,lelv), mul_dz(lx1,ly1,lz1,lelv)
      real mul, mul_dx, mul_dy, mul_dz

      real Fx, Fy

      n=nx1*ny1*nz1*nelv

      U0 = 1.
      pi = 4.0*atan(1.0)
      phx= pi/2.
      phy= pi/2.

      do i=1,n
        x = xm1(i,1,1,1)
        y = ym1(i,1,1,1)
        z = zm1(i,1,1,1)

        valmu = mul   (i,1,1,1)
        dmudx = mul_dx(i,1,1,1)
        dmudy = mul_dy(i,1,1,1)
        dmudz = mul_dz(i,1,1,1)
  
        Uvec  = (2.*pi*U0   ) * sin(pi*x+phx) * sin(2.*pi*y+phy)
        Vvec  = (3.*pi*U0/2.) * cos(pi*x+phx) * cos(2.*pi*y+phy)
        Wvec  =  0.
  
        xterm =-(5.*pi**2*U0   ) * cos(pi*x+phx) * sin(2.*pi*y+phy)
        yterm = (5.*pi**2*U0/2.) * sin(pi*x+phx) * cos(2.*pi*y+phy)
        zterm =  0.

        Fx = -xterm * valmu - (- Uvec*dmudx + Vvec * dmudy)
        Fy = -yterm * valmu - (+ Vvec*dmudx + Uvec * dmudy)
        forcx(i,1,1,1) = Fx * cos(P_ROT) - Fy * sin(P_ROT)
        forcy(i,1,1,1) = Fx * sin(P_ROT) + Fy * cos(P_ROT)
        forcz(i,1,1,1) = -zterm * valmu
      enddo

      return
      end
c-----------------------------------------------------------------------
