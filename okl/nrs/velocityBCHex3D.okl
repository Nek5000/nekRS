/*

   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */

#define surfaceTerms(sk,face,i, j)                                      \
  {                                                                     \
    const dfloat WsJ = sgeo[sk * p_Nsgeo + p_WSJID];                    \
    struct bcData bc;                                                   \
    bc.idM = vmapM[sk];   bc.time = time;                               \
    bc.nx = sgeo[sk * p_Nsgeo + p_NXID];                                \
    bc.ny = sgeo[sk * p_Nsgeo + p_NYID];                                \
    bc.nz = sgeo[sk * p_Nsgeo + p_NZID];                                \
    bc.x   = x[bc.idM];                                                 \
    bc.y   = y[bc.idM];                                                 \
    bc.z   = z[bc.idM];                                                 \
    bc.fieldOffset = offset;                                            \
    bc.id  = EToBM[face + p_Nfaces * e];                                \
    bc.u  = U[bc.idM + 0 * offset];                                     \
    bc.v  = U[bc.idM + 1 * offset];                                     \
    bc.w  = U[bc.idM + 2 * offset];                                     \
    bc.wrk = W;                                                         \
    dfloat TRx = 0.f;                                                   \
    dfloat TRy = 0.f;                                                   \
    dfloat TRz = 0.f;                                                   \
    const dlong bcType = EToB[face + p_Nfaces * e];                     \
    if(bcType == 3) {                                                   \
        bc.p = 0.f;                                                     \
        pressureDirichletConditions(&bc);                               \
        TRx = -bc.p*bc.nx;                                              \
        TRy = -bc.p*bc.ny;                                              \
        TRz = -bc.p*bc.nz;                                              \
    }                                                                   \
    s_ndU[j][i] = WsJ*TRx;                                              \
    s_ndV[j][i] = WsJ*TRy;                                              \
    s_ndW[j][i] = WsJ*TRz;                                              \
  }

//RHS contributions for continuous solver
@kernel void velocityNeumannBCHex3D(const dlong Nelements,
                                       const dlong offset,
                                       @restrict const dfloat*  sgeo,
                                       @restrict const dlong*  vmapM,
                                       @restrict const int*  EToBM,
                                       @restrict const int*  EToB,
                                       const dfloat time,
                                       @restrict const dfloat*  x,
                                       @restrict const dfloat*  y,
                                       @restrict const dfloat*  z,
                                       @restrict const dfloat*  W,
                                       @restrict const dfloat*  U,
                                       @restrict dfloat*  rhsU)
{
  for(dlong e = 0; e < Nelements; ++e; @outer(0)) {
    @shared dfloat s_ndU[p_Nq][p_Nq];
    @shared dfloat s_ndV[p_Nq][p_Nq];
    @shared dfloat s_ndW[p_Nq][p_Nq];
    @exclusive dfloat r_rhsU[p_Nq], r_rhsV[p_Nq], r_rhsW[p_Nq];

    // for all face nodes of all elements
    // face 0
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
#pragma unroll p_Nq
        for(int k = 0; k < p_Nq; ++k) {
          r_rhsU[k] = 0.;
          r_rhsV[k] = 0.;
          r_rhsW[k] = 0.;
        }

        const dlong sk0 = e * p_Nfp * p_Nfaces + 0 * p_Nfp + i + j * p_Nq;
        surfaceTerms(sk0,0,i,j);
      }
    }

    @barrier("local");

    // face 0
    for(int j = 0; j < p_Nq; ++j; @inner(1))
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        //face 0
        if(EToBM[e * p_Nfaces + 0] > 0) {
          r_rhsU[0] += s_ndU[j][i];
          r_rhsV[0] += s_ndV[j][i];
          r_rhsW[0] += s_ndW[j][i];
        }
      }

    @barrier("local");

    // face 5
    for(int j = 0; j < p_Nq; ++j; @inner(1))
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const dlong sk5 = e * p_Nfp * p_Nfaces + 5 * p_Nfp + i + j * p_Nq;
        surfaceTerms(sk5,5,i,j);
      }

    @barrier("local");

    // face 5
    for(int j = 0; j < p_Nq; ++j; @inner(1))
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        //face 5
        if(EToBM[e * p_Nfaces + 5] > 0) {
          r_rhsU[p_Nq - 1] += s_ndU[j][i];
          r_rhsV[p_Nq - 1] += s_ndV[j][i];
          r_rhsW[p_Nq - 1] += s_ndW[j][i];
        }
      }

    @barrier("local");

    // face 1
    for(int k = 0; k < p_Nq; ++k; @inner(1))
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const dlong sk1 = e * p_Nfp * p_Nfaces + 1 * p_Nfp + i + k * p_Nq;
        surfaceTerms(sk1,1,i,k);
      }

    @barrier("local");

    // face 1
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        if (j == 0) {//face 1
#pragma unroll p_Nq
          for (int k = 0; k < p_Nq; k++) {
            if(EToBM[e * p_Nfaces + 1] > 0) {
              r_rhsU[k] += s_ndU[k][i];
              r_rhsV[k] += s_ndV[k][i];
              r_rhsW[k] += s_ndW[k][i];
            }
          }
        }
      }
    }

    @barrier("local");

    // face 3
    for(int k = 0; k < p_Nq; ++k; @inner(1))
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        const dlong sk3 = e * p_Nfp * p_Nfaces + 3 * p_Nfp + i + k * p_Nq;
        surfaceTerms(sk3,3,i,k);
      }

    @barrier("local");

    // face 3
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        if (j == p_Nq - 1) {//face 3
#pragma unroll p_Nq
          for (int k = 0; k < p_Nq; k++) {
            if(EToBM[e * p_Nfaces + 3] > 0) {
              r_rhsU[k] += s_ndU[k][i];
              r_rhsV[k] += s_ndV[k][i];
              r_rhsW[k] += s_ndW[k][i];
            }
          }
        }
      }
    }

    @barrier("local");

    // face 2
    for(int k = 0; k < p_Nq; ++k; @inner(1))
      for(int j = 0; j < p_Nq; ++j; @inner(0)) {
        const dlong sk2 = e * p_Nfp * p_Nfaces + 2 * p_Nfp + j + k * p_Nq;
        surfaceTerms(sk2,2,j,k);
      }

    @barrier("local");

    // face 2
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        if (i == p_Nq - 1) {//face 2
#pragma unroll p_Nq
          for (int k = 0; k < p_Nq; k++) {
            if(EToBM[e * p_Nfaces + 2] > 0) {
              r_rhsU[k] += s_ndU[k][j];
              r_rhsV[k] += s_ndV[k][j];
              r_rhsW[k] += s_ndW[k][j];
            }
          }
        }
      }
    }

    @barrier("local");

    // face 4
    for(int k = 0; k < p_Nq; ++k; @inner(1))
      for(int j = 0; j < p_Nq; ++j; @inner(0)) {
        const dlong sk4 = e * p_Nfp * p_Nfaces + 4 * p_Nfp + j + k * p_Nq;
        surfaceTerms(sk4,4,j,k);
      }

    @barrier("local");

    // face 4
    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
        if (i == 0) {//face 4
#pragma unroll p_Nq
          for (int k = 0; k < p_Nq; k++) {
            if(EToBM[e * p_Nfaces + 4] > 0) {
              r_rhsU[k] += s_ndU[k][j];
              r_rhsV[k] += s_ndV[k][j];
              r_rhsW[k] += s_ndW[k][j];
            }
          }
        }
      }
    }

    @barrier("local");

    for(int j = 0; j < p_Nq; ++j; @inner(1)) {
      for(int i = 0; i < p_Nq; ++i; @inner(0)) {
#pragma unroll p_Nq
        for(int k = 0; k < p_Nq; k++) {
          const dlong id = e * p_Np + k * p_Nq * p_Nq + j * p_Nq + i;
          rhsU[id + 0 * offset] += r_rhsU[k];
          rhsU[id + 1 * offset] += r_rhsV[k];
          rhsU[id + 2 * offset] += r_rhsW[k];
        }
      }
    }
  }
}

@kernel void velocityDirichletBCHex3D(const dlong Nelements,
                                         const dlong offset,
                                         const dfloat time,
                                         @restrict const dfloat*  sgeo,
                                         @restrict const dfloat*  x,
                                         @restrict const dfloat*  y,
                                         @restrict const dfloat*  z,
                                         @restrict const dlong*  vmapM,
                                         @restrict const int*  EToBM,
                                         @restrict const int*  EToB,
                                         @restrict const int*  EToBV,
                                         @restrict const dlong*  neknekPointMap,
                                         @restrict const dfloat*  neknekValInterp,
                                         @restrict const dfloat*  W,
                                         @restrict const dfloat*  U,
                                         @restrict dfloat*  UH)
{
  for(dlong e = 0; e < Nelements; e++; @outer(0))
    for(int f = 0; f < p_Nfaces; f++) {
      @barrier("global");
      for(int m = 0; m < p_Nfp; ++m; @inner(0)) {
        struct bcData bc;
        const int n = m + f * p_Nfp;
        const int sk = e * p_Nfp * p_Nfaces + n;
        const dlong sid  = e * p_Nfaces * p_Nfp + n;
        const dlong idM = vmapM[sk];
        const dlong bcType = EToB[f + p_Nfaces * e];
        const dlong bcTypeV = EToBV[idM];
        const dlong neknekOffset = neknekPointMap[offset];

        if(bcType > 0) {
          bc.id = EToBM[f + p_Nfaces * e];
          bc.idM = idM;
          bc.time = time;
          bc.x  = x[idM];
          bc.y  = y[idM];
          bc.z  = z[idM];

          bc.nx = sgeo[sid * p_Nsgeo + p_NXID];
          bc.ny = sgeo[sid * p_Nsgeo + p_NYID];
          bc.nz = sgeo[sid * p_Nsgeo + p_NZID];

          bc.u = U[idM + 0 * offset];
          bc.v = U[idM + 1 * offset];
          bc.w = U[idM + 2 * offset];

          bc.wrk = W;
          bc.fieldOffset = offset;

          if(bcType == 1) {
            UH[idM + 0 * offset] = 0.0;
            UH[idM + 1 * offset] = 0.0;
            UH[idM + 2 * offset] = 0.0;
          }else if(bcType == 2 && bcTypeV == bcType) {
            if (neknekOffset > 0 && neknekPointMap[idM] >= 0) {
              bc.u = neknekValInterp[neknekPointMap[idM] + 0*neknekOffset];
              bc.v = neknekValInterp[neknekPointMap[idM] + 1*neknekOffset];
              bc.w = neknekValInterp[neknekPointMap[idM] + 2*neknekOffset];
            } else {
              velocityDirichletConditions(&bc);
            }
            UH[idM + 0 * offset] = bc.u;
            UH[idM + 1 * offset] = bc.v;
            UH[idM + 2 * offset] = bc.w;
          }else if(bcType == 3 && bcTypeV == bcType) {
            UH[idM + 0 * offset] = bc.u;
            UH[idM + 1 * offset] = bc.v;
            UH[idM + 2 * offset] = bc.w;
          }else if(bcType == 4 && bcTypeV == bcType) {
            UH[idM + 0 * offset] = 0.0;
          }else if(bcType == 5 && bcTypeV == bcType) {
            UH[idM + 1 * offset] = 0.0;
          }else if(bcType == 6 && bcTypeV == bcType) {
            UH[idM + 2 * offset] = 0.0;
          }
        }
      }
    }
}
