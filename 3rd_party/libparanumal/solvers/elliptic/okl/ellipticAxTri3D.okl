/*

  The MIT License (MIT)

  Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.

*/

 
@kernel void ellipticAxTri3D(const dlong Nelements,
			     @restrict const  dfloat *  ggeo,
			     @restrict const  dfloat *  Dmatrices,
			     @restrict const  dfloat *  Smatrices,
			     @restrict const  dfloat *  MM,
			     const dfloat lambda,
			     @restrict const  dfloat  *  q,
			     @restrict dfloat  *  Aq){
  
  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){

    @shared dfloat s_q[p_NblockV][p_Np];

    for(dlong e=eo;e<eo+p_NblockV;++e;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){
        if (e<Nelements) {
          //prefetch q
          const dlong id = n + e*p_Np;
          s_q[e-eo][n] = q[id];
        }
      }
    }

    @barrier("local");


    for(dlong e=eo;e<eo+p_NblockV;++e;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){
        if (e<Nelements) {
          const dlong es = e-eo;
          const dlong gid = e*p_Nggeo;

          const dfloat Grr = ggeo[gid + p_G00ID];
          const dfloat Grs = ggeo[gid + p_G01ID];
          const dfloat Gss = ggeo[gid + p_G11ID];
          const dfloat J   = ggeo[gid + p_GWJID];

          dfloat qrr = 0.;
          dfloat qrs = 0.;
          dfloat qss = 0.;
          dfloat qM = 0.;

#pragma unroll p_Np
	  for (int k=0;k<p_Np;k++) {
	    dfloat qn = s_q[es][k];
	    qrr += Smatrices[n+k*p_Np+0*p_Np*p_Np]*qn;
	    qrs += Smatrices[n+k*p_Np+1*p_Np*p_Np]*qn;
	    qss += Smatrices[n+k*p_Np+2*p_Np*p_Np]*qn;
	    qM  += MM[n+k*p_Np]*s_q[es][k];
	  }


          const dlong id = n + e*p_Np;

          Aq[id] = Grr*qrr+Grs*qrs+Gss*qss + J*lambda*qM;
        }
      }
    }
  }
}
//Analysis:
// We perform (per thread block)
// Nelements per block x Np x (Np x 5+10) flops
// We load: (Nelements per block) x Np to @shared
// We read (Nelements per block) x Np xNp x 5 times from @shared
// We request: (Nelements per block) x Np x(1+4+Npx5 variables
// We store (Nelements per block) x Np x (1) variables


@kernel void ellipticPartialAxTri3D(const dlong Nelements,
				    @restrict const  dlong   *  elementList,
				    @restrict const  dfloat *  ggeo,
				    @restrict const  dfloat *  Dmatrices,
				    @restrict const  dfloat *  Smatrices,
				    @restrict const  dfloat *  MM,
				    const dfloat lambda,
				    @restrict const  dfloat  *  q,
				    @restrict dfloat  *  Aq){
  
  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){

    @shared dfloat s_q[p_NblockV][p_Np];

    for(dlong e=eo;e<eo+p_NblockV;++e;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){
        if (e<Nelements) {
          //prefetch q
          const dlong element = elementList[e];
          const dlong id = n + element*p_Np;
          s_q[e-eo][n] = q[id];
        }
      }
    }

    @barrier("local");


    for(dlong e=eo;e<eo+p_NblockV;++e;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){
        if (e<Nelements) {
          const dlong es = e-eo;
          const dlong element = elementList[e];
          const dlong gid = element*p_Nggeo;

          const dfloat Grr = ggeo[gid + p_G00ID];
          const dfloat Grs = ggeo[gid + p_G01ID];
          const dfloat Gss = ggeo[gid + p_G11ID];
          const dfloat J   = ggeo[gid + p_GWJID];

          dfloat qrr = 0.;
          dfloat qrs = 0.;
          dfloat qss = 0.;
          dfloat qM = 0.;

#pragma unroll p_Np
	  for (int k=0;k<p_Np;k++) {
	    dfloat qn = s_q[es][k];
	    qrr += Smatrices[n+k*p_Np+0*p_Np*p_Np]*qn;
	    qrs += Smatrices[n+k*p_Np+1*p_Np*p_Np]*qn;
	    qss += Smatrices[n+k*p_Np+2*p_Np*p_Np]*qn;
	    qM  += MM[n+k*p_Np]*s_q[es][k];
	  }
            
          const dlong id = n + element*p_Np;

          Aq[id] = Grr*qrr+Grs*qrs+Gss*qss + J*lambda*qM;
        }
      }
    }
  }
}
