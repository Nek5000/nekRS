/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/


// rhsU^s = MM*(U^n - \sum^s-1 ea_si N(U^i) + \sum^s-1 ia_si LU^i - \sum^s-1 pa_si GP^i)/ia_ss nu dt
@kernel void cdsHelmholtzRhsEXTBDFHex3D(const dlong Nelements,
                                @restrict const  dfloat *  vgeo,
                                @restrict const  dfloat *  MM,
                                const dfloat idt,
                                const dfloat ialf,
                                @restrict const  dfloat *  extbdfA,
                                @restrict const  dfloat *  extbdfB,
                                @restrict const  dfloat *  extbdfC,
                                const dlong sOffset,
                                @restrict const  dfloat *  S,
                                @restrict const  dfloat *  NS,
                                @restrict const  dfloat *  FS,
                                      @restrict dfloat *  rhsS){

  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){

    for(int es=0;es<p_NblockV;++es;@inner(2)){// for all elements in block
      for(int j=0;j<p_Nq;++j;@inner(1)){ 
        for(int i=0;i<p_Nq;++i;@inner(0)){
          const dlong e = eo+es; // element in block
          if(e<Nelements){
            #pragma unroll p_Nq
            for(int k=0;k<p_Nq;++k){
              const dlong id  = i + j*p_Nq + k*p_Nq*p_Nq + p_Np*e;
              const dlong gid = i + j*p_Nq + k*p_Nq*p_Nq + e*p_Np*p_Nvgeo;
              const dfloat JW = vgeo[gid + p_JWID*p_Np];

              dfloat rhsSn;

              if (p_SUBCYCLING) {
                //NU holds \hat{U} after subcycling
                const dfloat NSm = NS[id+0*sOffset];
                rhsSn = JW*ialf*idt*NSm;
                 for (int s=0;s<p_Nstages;s++) {
                  rhsSn -= JW*ialf*extbdfA[s]*FS[id+0*sOffset+s*p_NSfields*sOffset];
                }
              }else {
                rhsSn = 0.f;
               for (int s=0;s<p_Nstages;s++) {
                  // S 
                  const dfloat Sm  = S[id+0*sOffset+s*p_NSfields*sOffset];
                  // NS
                  const dfloat NSm = NS[id+0*sOffset+s*p_NSfields*sOffset];
                  const dfloat FSm = FS[id+0*sOffset+s*p_NSfields*sOffset];
                  rhsSn += JW*ialf*(idt*extbdfB[s]*Sm - extbdfA[s]*(NSm + FSm));
                }
              }
              rhsS[id] = rhsSn;
            }
          }
        }
      }
    }
  }
}
