/*

  The MIT License (MIT)

  Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.

*/
@kernel void insStrongAdvectionVolumeHex3D(const dlong Nelements,
					   @restrict const  dfloat *  vgeo,
					   @restrict const  dfloat *  D,
					   const dlong offset,
					   @restrict const  dfloat *  U,
					   @restrict dfloat *  NU){

  for(dlong e=0; e<Nelements; ++e; @outer(0)) {
    @shared dfloat s_U[p_Nq][p_Nq][p_Nq];
    @shared dfloat s_V[p_Nq][p_Nq][p_Nq];
    @shared dfloat s_W[p_Nq][p_Nq][p_Nq];

    @shared dfloat s_D[p_Nq][p_Nq];

    for(int k=0;k<p_Nq;++k;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
	for(int i=0;i<p_Nq;++i;@inner(0)){
	  dlong id = i+j*p_Nq;
	  if(k==0)
	    s_D[0][id] = D[id];

	  id = e*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;

	  const dfloat Un = U[id+0*offset];
	  const dfloat Vn = U[id+1*offset];
	  const dfloat Wn = U[id+2*offset];

	  s_U[k][j][i] = Un;
	  s_V[k][j][i] = Vn;
	  s_W[k][j][i] = Wn;
	}
      }
    }

    @barrier("local");

    for(int k=0;k<p_Nq;++k;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
	for(int i=0;i<p_Nq;++i;@inner(0)){

	  const dlong gid = e*p_Np*p_Nvgeo+ k*p_Nq*p_Nq + j*p_Nq +i;
	  const dfloat drdx = vgeo[gid + p_RXID*p_Np];
	  const dfloat drdy = vgeo[gid + p_RYID*p_Np];
	  const dfloat drdz = vgeo[gid + p_RZID*p_Np];
	  const dfloat dsdx = vgeo[gid + p_SXID*p_Np];
	  const dfloat dsdy = vgeo[gid + p_SYID*p_Np];
	  const dfloat dsdz = vgeo[gid + p_SZID*p_Np];
	  const dfloat dtdx = vgeo[gid + p_TXID*p_Np];
	  const dfloat dtdy = vgeo[gid + p_TYID*p_Np];
	  const dfloat dtdz = vgeo[gid + p_TZID*p_Np];

	  dfloat dudr = 0, duds = 0, dudt = 0;
	  dfloat dvdr = 0, dvds = 0, dvdt = 0;
	  dfloat dwdr = 0, dwds = 0, dwdt = 0;

#pragma unroll p_Nq
	  for (int n=0;n<p_Nq;n++) {
	    const dfloat Dr = s_D[i][n];
	    const dfloat Ds = s_D[j][n];
	    const dfloat Dt = s_D[k][n];
	    dudr += Dr*s_U[k][j][n];
	    duds += Ds*s_U[k][n][i];
	    dudt += Dt*s_U[n][j][i];

	    dvdr += Dr*s_V[k][j][n];
	    dvds += Ds*s_V[k][n][i];
	    dvdt += Dt*s_V[n][j][i];

	    dwdr += Dr*s_W[k][j][n];
	    dwds += Ds*s_W[k][n][i];
	    dwdt += Dt*s_W[n][j][i];
	  }

	  dfloat dudx = drdx*dudr + dsdx*duds + dtdx*dudt;
	  dfloat dudy = drdy*dudr + dsdy*duds + dtdy*dudt;
	  dfloat dudz = drdz*dudr + dsdz*duds + dtdz*dudt;

	  dfloat dvdx = drdx*dvdr + dsdx*dvds + dtdx*dvdt;
	  dfloat dvdy = drdy*dvdr + dsdy*dvds + dtdy*dvdt;
	  dfloat dvdz = drdz*dvdr + dsdz*dvds + dtdz*dvdt;

	  dfloat dwdx = drdx*dwdr + dsdx*dwds + dtdx*dwdt;
	  dfloat dwdy = drdy*dwdr + dsdy*dwds + dtdy*dwdt;
	  dfloat dwdz = drdz*dwdr + dsdz*dwds + dtdz*dwdt;
	  
	  dfloat u = s_U[k][j][i];
	  dfloat v = s_V[k][j][i];
	  dfloat w = s_W[k][j][i];

	  const dlong id =e*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;

	  NU[id+0*offset] =  (u*dudx + v*dudy + w*dudz);
	  NU[id+1*offset] =  (u*dvdx + v*dvdy + w*dvdz);
	  NU[id+2*offset] =  (u*dwdx + v*dwdy + w*dwdz);
	}
      }
    }
  }
}

@kernel void insStrongAdvectionCubatureVolumeHex3D(const dlong Nelements,
						   @restrict const  dfloat *  vgeo,
						   @restrict const  dfloat *  cubvgeo,
						   @restrict const  dfloat *  cubD,
						   @restrict const  dfloat *  cubInterpT,
						   @restrict const  dfloat *  cubProjectT,
						   const dlong offset,
						   @restrict const  dfloat *  U,
						   @restrict dfloat *  NU){
  
  // (phi, U.grad Ud)
  for(dlong e=0; e<Nelements; ++e; @outer(0)) {

    
    @shared dfloat s_cubD[p_cubNq][p_cubNq];
    @shared dfloat s_cubInterpT[p_Nq][p_cubNq];
    @shared dfloat s_cubProjectT[p_cubNq][p_Nq];
    
    @shared dfloat s_U[p_cubNq][p_cubNq];
    @shared dfloat s_V[p_cubNq][p_cubNq];
    @shared dfloat s_W[p_cubNq][p_cubNq];

    @shared dfloat s_U1[p_cubNq][p_cubNq];
    @shared dfloat s_V1[p_cubNq][p_cubNq];
    @shared dfloat s_W1[p_cubNq][p_cubNq];

    @exclusive dfloat r_U [p_cubNq], r_V [p_cubNq], r_W [p_cubNq];
    @exclusive dfloat r_Ud[p_cubNq], r_Vd[p_cubNq], r_Wd[p_cubNq];

    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        const int id = i+j*p_cubNq;
        if (id<p_Nq*p_cubNq) {
          s_cubInterpT[0][id] = cubInterpT[id];
          s_cubProjectT[0][id] = cubProjectT[id];
        }
  
        s_cubD[0][id] = cubD[id];
  
	for(int k=0;k<p_cubNq;++k){
	  r_U[k]  = 0; r_V[k]  = 0; r_W[k]  = 0;
	  r_Ud[k] = 0; r_Vd[k] = 0; r_Wd[k] = 0;
	}
      }
    }

    for(int c=0;c<p_Nq;++c){

      @barrier("local");

      for(int b=0;b<p_cubNq;++b;@inner(1)){
	for(int a=0;a<p_cubNq;++a;@inner(0)){
	  if(a<p_Nq && b<p_Nq){
	    // this can be improved
	    const dlong id = e*p_Np + c*p_Nq*p_Nq + b*p_Nq + a;
	    // U.grad U
	    s_U[b][a] = U[id+0*offset]; 
	    s_V[b][a] = U[id+1*offset];
	    s_W[b][a] = U[id+2*offset];      
	  }
	}
      }
      
      @barrier("local");
      
      // interpolate in 'r' 
      for(int b=0;b<p_cubNq;++b;@inner(1)){
	for(int i=0;i<p_cubNq;++i;@inner(0)){
	  if(b<p_Nq){
	    dfloat U1  = 0, V1 = 0,  W1 = 0;
	    for(int a=0;a<p_Nq;++a){
	      dfloat Iia = s_cubInterpT[a][i];
	      U1  += Iia*s_U[b][a];
	      V1  += Iia*s_V[b][a];
	      W1  += Iia*s_W[b][a];
	    }

	    s_U1[b][i] = U1;
	    s_V1[b][i] = V1;
	    s_W1[b][i] = W1;      
	  }
	}
      }

      @barrier("local");

      // interpolate in 's' 
      for(int j=0;j<p_cubNq;++j;@inner(1)){
	for(int i=0;i<p_cubNq;++i;@inner(0)){
	  dfloat U2 = 0, V2 = 0,  W2 = 0;
	  // interpolate in b
	  for(int b=0;b<p_Nq;++b){
	    dfloat Ijb = s_cubInterpT[b][j];
	    U2 += Ijb*s_U1[b][i];
	    V2 += Ijb*s_V1[b][i];
	    W2 += Ijb*s_W1[b][i];
	  }

	  // interpolate in c progressively
	  for(int k=0;k<p_cubNq;++k){
	    dfloat Ikc = s_cubInterpT[c][k];
	    r_U[k] += Ikc*U2;
	    r_V[k] += Ikc*V2;
	    r_W[k] += Ikc*W2;
	  }

	  for(int k=0;k<p_cubNq;++k){
	    r_Ud[k] = r_U[k]; 
	    r_Vd[k] = r_V[k]; 
	    r_Wd[k] = r_W[k]; 
	  }
	}
      }
    }

    for(int k=0;k<p_cubNq;++k){

      @barrier("local");

      for(int j=0;j<p_cubNq;++j;@inner(1)){
	for(int i=0;i<p_cubNq;++i;@inner(0)){
    
	  s_U1[j][i] = r_Ud[k];
	  s_V1[j][i] = r_Vd[k];
	  s_W1[j][i] = r_Wd[k];

	}
      }

      @barrier("local");

      for(int j=0;j<p_cubNq;++j;@inner(1)){
	for(int i=0;i<p_cubNq;++i;@inner(0)){

	  dfloat  Udr = 0, Uds = 0, Udt = 0;
	  dfloat  Vdr = 0, Vds = 0, Vdt = 0;
	  dfloat  Wdr = 0, Wds = 0, Wdt = 0;

	  for(int n=0;n<p_cubNq;++n){
	    dfloat Din = s_cubD[i][n];
	    Udr += Din*s_U1[j][n];
	    Vdr += Din*s_V1[j][n];
	    Wdr += Din*s_W1[j][n];
	  }

	  for(int n=0;n<p_cubNq;++n){
	    dfloat Djn = s_cubD[j][n];
	    Uds += Djn*s_U1[n][i];
	    Vds += Djn*s_V1[n][i];
	    Wds += Djn*s_W1[n][i];
	  }
    
	  for(int n=0;n<p_cubNq;++n){
	    dfloat Dkn = s_cubD[k][n];
	    Udt += Dkn*r_Ud[n];
	    Vdt += Dkn*r_Vd[n];
	    Wdt += Dkn*r_Wd[n];     
	  }
    
          const dlong gid = e*p_cubNp*p_Nvgeo+ k*p_cubNq*p_cubNq + j*p_cubNq +i;
          const dfloat drdx = cubvgeo[gid + p_RXID*p_cubNp];
          const dfloat drdy = cubvgeo[gid + p_RYID*p_cubNp];
          const dfloat drdz = cubvgeo[gid + p_RZID*p_cubNp];
          const dfloat dsdx = cubvgeo[gid + p_SXID*p_cubNp];
          const dfloat dsdy = cubvgeo[gid + p_SYID*p_cubNp];
          const dfloat dsdz = cubvgeo[gid + p_SZID*p_cubNp];
          const dfloat dtdx = cubvgeo[gid + p_TXID*p_cubNp];
          const dfloat dtdy = cubvgeo[gid + p_TYID*p_cubNp];
          const dfloat dtdz = cubvgeo[gid + p_TZID*p_cubNp];
          const dfloat JW   = cubvgeo[gid + p_JWID*p_cubNp];
    
          const dfloat Un = r_U[k];
          const dfloat Vn = r_V[k];
          const dfloat Wn = r_W[k];

	  const dfloat Uhat = JW*(Un*drdx + Vn*drdy + Wn*drdz);
	  const dfloat Vhat = JW*(Un*dsdx + Vn*dsdy + Wn*dsdz);
	  const dfloat What = JW*(Un*dtdx + Vn*dtdy + Wn*dtdz);

	  // I_f^t*(J_f*C_f^t)*G_f*\hat{D}_f*I_f*u
	  r_U[k] = Uhat*Udr + Vhat*Uds + What*Udt;
	  r_V[k] = Uhat*Vdr + Vhat*Vds + What*Vdt;
	  r_W[k] = Uhat*Wdr + Vhat*Wds + What*Wdt;
	}
      }
    }
    
    // now project back in t
    for(int c=0;c<p_Nq;++c){

      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){
	for(int i=0;i<p_cubNq;++i;@inner(0)){
    
	  dfloat rhsU = 0, rhsV = 0, rhsW = 0;

	  for(int k=0;k<p_cubNq;++k){
	    dfloat Ikc = s_cubInterpT[c][k];
	    rhsU += Ikc*r_U[k];
	    rhsV += Ikc*r_V[k];
	    rhsW += Ikc*r_W[k];     
	  }
    
	  s_U[j][i] = rhsU;
	  s_V[j][i] = rhsV;
	  s_W[j][i] = rhsW;
	}
      }

      @barrier("local");
      
      for(int b=0;b<p_cubNq;++b;@inner(1)){
	for(int i=0;i<p_cubNq;++i;@inner(0)){
	  if(b<p_Nq){
	    dfloat rhsU = 0, rhsV = 0, rhsW = 0;

	    for(int j=0;j<p_cubNq;++j){
	      dfloat Ijb = s_cubInterpT[b][j];
	      rhsU += Ijb*s_U[j][i];
	      rhsV += Ijb*s_V[j][i];
	      rhsW += Ijb*s_W[j][i];
	    }

	    s_U1[b][i] = rhsU;
	    s_V1[b][i] = rhsV;
	    s_W1[b][i] = rhsW;
	  }
	}
      }
      
      @barrier("local");
      
      for(int b=0;b<p_cubNq;++b;@inner(1)){
	for(int a=0;a<p_cubNq;++a;@inner(0)){
	  if(a<p_Nq && b<p_Nq){
	    dfloat rhsU = 0, rhsV = 0, rhsW = 0;

	    for(int i=0;i<p_cubNq;++i){
	      dfloat Iia = s_cubInterpT[a][i];
	      rhsU += Iia*s_U1[b][i];
	      rhsV += Iia*s_V1[b][i];
	      rhsW += Iia*s_W1[b][i];
	    }
            const dlong gid = e*p_Np*p_Nvgeo+ c*p_Nq*p_Nq + b*p_Nq +a;
	    const dfloat IJW   = vgeo[gid + p_IJWID*p_Np];

            const dlong id = e*p_Np + c*p_Nq*p_Nq + b*p_Nq + a;
            NU[id+0*offset] = IJW*rhsU;
            NU[id+1*offset] = IJW*rhsV;
            NU[id+2*offset] = IJW*rhsW;
          }
        }
      }
    }
  }
}
