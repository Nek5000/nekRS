/*

   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */
@kernel void ellipticBlockUpdatePCG(const dlong N,
                                    const dlong offset,
                                    @restrict const dfloat* invDegree,
                                    @restrict const dfloat* Ap,
                                    const dfloat alpha,
                                    @restrict dfloat* r,
                                    @restrict dfloat* redr)
{
  for(dlong b = 0; b < (N+p_blockSize-1)/p_blockSize; ++b; @outer(0)) {
    @shared volatile dfloat s_sum[p_blockSize];

    for(int t = 0; t < p_blockSize; ++t; @inner(0)) {
        int n = t + b * p_blockSize;
        s_sum[t] = 0;
        if(n < N) {
          dfloat sum = 0.0;
          #pragma unroll
          for(int fld = 0; fld < p_Nfields; fld++) {
            dfloat rn = r[n + fld * offset];
 
            const dfloat Apn = Ap[n + fld * offset];
 
            rn  -= alpha * Apn;
            sum += rn * rn;
 
            r[n + fld * offset] = rn;
          }
          s_sum[t] = sum*invDegree[n];
        }
    }

    @barrier("local");
#if p_blockSize>512
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<512) s_sum[t] += s_sum[t+512];
    @barrier("local");
#endif

#if p_blockSize>256
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<256) s_sum[t] += s_sum[t+256];
    @barrier("local");
#endif

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<128) s_sum[t] += s_sum[t+128];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 64) s_sum[t] += s_sum[t+ 64];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 32) s_sum[t] += s_sum[t+ 32];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 16) s_sum[t] += s_sum[t+ 16];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  8) s_sum[t] += s_sum[t+  8];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  4) s_sum[t] += s_sum[t+  4];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  2) s_sum[t] += s_sum[t+  2];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  1) redr[b] = s_sum[0] + s_sum[1];
  }
}
