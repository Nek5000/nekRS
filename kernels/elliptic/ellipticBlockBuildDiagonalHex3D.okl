/*

   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */
#define p_MaxNFields (3)
@kernel void ellipticBlockBuildDiagonalHex3D(const dlong Nelements,
                                             const dlong Nfields,
                                             const dlong offset,
                                             const dlong loffset,
                                             @ restrict const dfloat *ggeo,
                                             @ restrict const dfloat *D,
                                             @ restrict const dfloat *S,
                                             @ restrict const dfloat *lambda0,
                                             @ restrict const dfloat *lambda1,
                                             @ restrict dfloat *Aq)
{
  for (dlong e = 0; e < Nelements; ++e; @outer(0)) {
    @shared dfloat s_D[p_Nq][p_Nq];

    @shared dfloat s_lambda0[p_MaxNFields][p_Nq][p_Nq];
    @shared dfloat s_Grr[p_Nq][p_Nq];
    @shared dfloat s_Gss[p_Nq][p_Nq];
    @exclusive dfloat s_Gtt[p_Nq];
    @exclusive dfloat s_lambdat[p_MaxNFields][p_Nq];

#ifdef smXX
#pragma unroll p_Nq
#endif
    for (int k = 0; k < p_Nq; ++k) {
      for (int j = 0; j < p_Nq; ++j; @inner(1)) {
        for (int i = 0; i < p_Nq; ++i; @inner(0)) {
          const dlong id = e * p_Np + k * p_Nq * p_Nq + j * p_Nq + i;
          const dlong base = e * p_Nggeo * p_Np + k * p_Nq * p_Nq + j * p_Nq + i;

          if (k == 0)
            s_D[j][i] = D[i + p_Nq * j]; // column major

          s_Grr[j][i] = ggeo[base + p_G00ID * p_Np];
          s_Gss[j][i] = ggeo[base + p_G11ID * p_Np];

          for (int l = 0; l < Nfields; l++)
            s_lambda0[l][j][i] = lambda0[id + l * loffset];
          if (k == 0) {
#pragma unroll p_Nq
            for (int l = 0; l < p_Nq; ++l) {
              const dlong other_base = e * p_Nggeo * p_Np + l * p_Nq * p_Nq + j * p_Nq + i;
              const dlong other_id = e * p_Np + l * p_Nq * p_Nq + j * p_Nq + i;
              s_Gtt[l] = ggeo[other_base + p_G22ID * p_Np];
              for (int field = 0; field < Nfields; field++)
                s_lambdat[field][l] = lambda0[other_id + field * loffset];
            }
          }
        }
      }
      @barrier();

      for (int j = 0; j < p_Nq; ++j; @inner(1)) {
        for (int i = 0; i < p_Nq; ++i; @inner(0)) {
          const dlong id = e * p_Np + k * p_Nq * p_Nq + j * p_Nq + i;
          dfloat r_q = 1.0;

          for (int l = 0; l < Nfields; l++) {

            r_q = 0.0; // first make it zero
            const dlong base = e * p_Nggeo * p_Np + k * p_Nq * p_Nq + j * p_Nq + i;

            dfloat grs = ggeo[base + p_G01ID * p_Np];
            dfloat grt = ggeo[base + p_G02ID * p_Np];
            dfloat gst = ggeo[base + p_G12ID * p_Np];

            dfloat lbda_0 = s_lambda0[l][j][i];

            r_q += 2.0 * grs * lbda_0 * s_D[i][i] * s_D[j][j];
            r_q += 2.0 * grt * lbda_0 * s_D[i][i] * s_D[k][k];
            r_q += 2.0 * gst * lbda_0 * s_D[j][j] * s_D[k][k];
            //
            for (int m = 0; m < p_Nq; m++) {
              r_q += s_Grr[j][m] * s_lambda0[l][j][m] * s_D[m][i] * s_D[m][i];
              r_q += s_Gss[m][i] * s_lambda0[l][m][i] * s_D[m][j] * s_D[m][j];
              r_q += s_Gtt[m] * s_lambdat[l][m] * s_D[m][k] * s_D[m][k];
            }

#ifndef p_poisson
            dfloat gwJ = ggeo[base + p_GWJID * p_Np];
            dfloat lbda_1 = lambda1[id + l * loffset];
            r_q += gwJ * lbda_1;
#endif

            Aq[id + l * offset] = r_q;
          }
        }
      }
      @barrier();
    }
  }
}
